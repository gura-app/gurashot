#!/usr/bin/env gura
//=============================================================================
// gurashot
// Copyright (C) 2014 ypsitau
//=============================================================================
import(wx)
import(re)
import(yaml)
import(bmp)
import(gif)
import(jpeg)
import(png)
import(xpm)
import(msico)
import(cairo)
import(resource)
import(mswin)
import(units)
import(translate)

Version = '1.1.0'

//-----------------------------------------------------------------------------
// Control ID
//-----------------------------------------------------------------------------
[
	ID_Open, ID_Close, ID_ShowList, ID_Prev, ID_Next,
	ID_OutputPDF, ID_LaunchPDFViewer, ID_PrintService,
	ID_RotateL, ID_RotateR, ID_ColorAdjust, ID_TiltAdj,
	ID_FrostFrame, ID_CenterGuide, ID_CopyExtractedImage, ID_SaveExtractedImage,
	ID_Label, ID_Remove, ID_RenameLabel,
	ID_OutputOption,
	ID_Adjust,
] = wx.ID_HIGHEST..

//-----------------------------------------------------------------------------
// LayoutFrame
//-----------------------------------------------------------------------------
LayoutFrame = struct(purpose:string, htFrame:number, wdFrame:number,
					htUpperMgn:number, htFacePart:number, userDefFlag:boolean) {
	CalcSizeEx() = {
		[this.wdFrame + g.wdEdgeMgn * 2,  this.htFrame + g.htEdgeMgn * 2]
	}
	CalcImageGrid(wdCanvas:number, htCanvas:number) = {
		[wdFrameEx, htFrameEx] = this.CalcSizeEx()
		nx = int((wdCanvas - g.wdTrimMark) / (wdFrameEx + g.wdTrimMark))
		ny = int((htCanvas - g.htTrimMark - g.htMessage) / (htFrameEx + g.htTrimMark))
		[nx, ny]
	}
}

//-----------------------------------------------------------------------------
// PrintServiceInfo
//-----------------------------------------------------------------------------
PrintServiceInfo = struct(printServiceName:string,
						nAdjLines_a:number, nAdjLines_b:number,
						nAdjLines_c:number, nAdjLines_d:number)

//-----------------------------------------------------------------------------
// PaperSize
//-----------------------------------------------------------------------------
PaperSize = struct(name:string, longSide:number, shortSide:number, printServiceFlag:boolean) {
	margin:public = 5
	GetCanvasWidth(orientation:symbol) = {
		cond(orientation == `horizontal, this.longSide - margin * 2, this.shortSide - margin * 2)
	}
	GetCanvasHeight(orientation:symbol) = {
		cond(orientation == `horizontal, this.shortSide - margin * 2, this.longSide - margin * 2)
	}
	GetBetterOrientation(layoutFrame:LayoutFrame, orientation:symbol) = {
		(orientation != `auto) && return(orientation)
		[nx1, ny1] = layoutFrame.CalcImageGrid(this.GetCanvasWidth(`horizontal),
									this.GetCanvasHeight(`horizontal))
		n1 = nx1 * ny1
		[nx2, ny2] = layoutFrame.CalcImageGrid(this.GetCanvasWidth(`vertical),
									this.GetCanvasHeight(`vertical))
		n2 = nx2 * ny2
		if (n1 > n2) {
			`horizontal
		} elsif (n1 < n2) {
			`vertical
		} elsif (max(nx1, ny1) / min(nx1, ny1) <= max(nx2, ny2) / min(nx2, ny2)) {
			`horizontal
		} else {
			`vertical
		}
	}
}

//-----------------------------------------------------------------------------
// FrameCoord
//-----------------------------------------------------------------------------
FrameCoord = struct(xMarkTop:number, xMarkBtm:number, yMarkTop:number, yMarkBtm:number,
	xTop:number, xBtm:number, yTop:number, yBtm:number, width:number, height:number)

//-----------------------------------------------------------------------------
// ImageInfo
//-----------------------------------------------------------------------------
ImageInfo = class {
	public {
		label,			label_init
		fileName,		fileName_init
		rotate,			rotate_init
		xrateTop,		xrateTop_init
		xrateBtm,		xrateBtm_init
		yrateTop,		yrateTop_init
		yrateBtm,		yrateBtm_init
		tiltAdjFlag,	tiltAdjFlag_init
		colorTone,		colorTone_init
		brightness,		brigheness_init
		contrast,		contrast_init
		redness,		redness_init
		greenness,		greenness_init
		blueness,		blueness_init
		imgPhotoRaw,
	}
	label_init			= ''
	fileName_init		= ''
	rotate_init			= 0
	xrateTop_init		= .5
	xrateBtm_init		= .5
	yrateTop_init		= .1
	yrateBtm_init		= .6
	tiltAdjFlag_init	= false
	colorTone_init		= `color
	brightness_init		= 0
	contrast_init		= 0
	redness_init		= 0
	greenness_init		= 0
	blueness_init		= 0
	mapTblLenear		= [0..255]
	__init__() = {
		this.label			= label_init
		this.fileName		= fileName_init
		this.rotate			= rotate_init
		this.xrateTop		= xrateTop_init
		this.xrateBtm		= xrateBtm_init
		this.yrateTop		= yrateTop_init
		this.yrateBtm		= yrateBtm_init
		this.tiltAdjFlag	= tiltAdjFlag_init
		this.colorTone		= colorTone_init
		this.brightness		= brightness_init
		this.contrast		= contrast_init
		this.redness		= redness_init
		this.greenness		= greenness_init
		this.blueness		= blueness_init
		this.imgPhotoRaw	= nil
	}
	CalcFrameCoord(rcImg:wx.Rect, layoutFrame:LayoutFrame) = {
		htBtm = layoutFrame.htFrame - layoutFrame.htUpperMgn - layoutFrame.htFacePart
		xrateDiff = this.xrateBtm - this.xrateTop
		yrateDiff = this.yrateBtm - this.yrateTop
		xMarkTop = rcImg.x + rcImg.width * this.xrateTop
		xMarkBtm = rcImg.x + rcImg.width * this.xrateBtm
		yMarkTop = rcImg.y + rcImg.height * this.yrateTop
		yMarkBtm = rcImg.y + rcImg.height * this.yrateBtm
		xTop = rcImg.x + rcImg.width * (this.xrateTop - xrateDiff * layoutFrame.htUpperMgn / layoutFrame.htFacePart)
		xBtm = rcImg.x + rcImg.width * (this.xrateBtm + xrateDiff * htBtm / layoutFrame.htFacePart)
		yTop = rcImg.y + rcImg.height * (this.yrateTop - yrateDiff * layoutFrame.htUpperMgn / layoutFrame.htFacePart)
		yBtm = rcImg.y + rcImg.height * (this.yrateBtm + yrateDiff * htBtm / layoutFrame.htFacePart)
		height = math.sqrt((xBtm - xTop) ** 2 + (yBtm - yTop) ** 2)
		width = height * layoutFrame.wdFrame / layoutFrame.htFrame
		FrameCoord(xMarkTop, xMarkBtm, yMarkTop, yMarkBtm,
										xTop, xBtm, yTop, yBtm, width, height)
	}
	MakeEmphasisMapTbl(level:number) = {
		a = -int(level * 128 / 100)
		b = 255 + int(level * 127 / 100)
		mapTbl = int(interval(a, b, 256))
		mapTbl = conds(mapTbl < 0, 0, mapTbl)
		conds(mapTbl > 255, 255, mapTbl):list
	}
	AdjustColor(image:image) = {
		imgResult = image
		mapTbl_R = this.mapTblLenear
		mapTbl_G = this.mapTblLenear
		mapTbl_B = this.mapTblLenear
		applyFlag = false
		if (this.brightness != 0) {
			applyFlag = true
			if (this.brightness < 0) {
				mapTbl = int(interval(0, 255 + int(this.brightness * 255 / 100), 256)):list
			} else { // this.brightness > 0
				mapTbl = int(interval(int(this.brightness * 255 / 100), 255, 256)):list
			}
			mapTbl_R = mapTbl[mapTbl_R]
			mapTbl_G = mapTbl[mapTbl_G]
			mapTbl_B = mapTbl[mapTbl_B]
		}
		if (this.contrast != 0) {
			applyFlag = true
			mapTbl = this.MakeEmphasisMapTbl(this.contrast)
			mapTbl_R = mapTbl[mapTbl_R]
			mapTbl_G = mapTbl[mapTbl_G]
			mapTbl_B = mapTbl[mapTbl_B]
		}
		if (this.redness != 0) {
			applyFlag = true
			mapTbl = this.MakeEmphasisMapTbl(this.redness)
			mapTbl_R = mapTbl[mapTbl_R]
		}
		if (this.greenness != 0) {
			applyFlag = true
			mapTbl = this.MakeEmphasisMapTbl(this.greenness)
			mapTbl_G = mapTbl[mapTbl_G]
		}
		if (this.blueness != 0) {
			applyFlag = true
			mapTbl = this.MakeEmphasisMapTbl(this.blueness)
			mapTbl_B = mapTbl[mapTbl_B]
		}
		if (applyFlag) {
			imgResult = imgResult.mapcolorlevel(mapTbl_R, mapTbl_G, mapTbl_B)
		}
		if (this.colorTone == `mono) {
			imgResult.grayscale()
		} elsif (this.colorTone == `sepia) {
			mapTbl_R = int(interval(0, g.sepiaR, 256)):list
			mapTbl_G = int(interval(0, g.sepiaG, 256)):list
			mapTbl_B = int(interval(0, g.sepiaB, 256)):list
			imgResult.grayscale().mapcolorlevel(mapTbl_R, mapTbl_G, mapTbl_B)
		} else {
			imgResult
		}
	}
}

//-----------------------------------------------------------------------------
// Global variables
//-----------------------------------------------------------------------------
g = module {
	fileNameTmpPDF:public	= path.join(sys.workdir, 'gurashot.pdf')
	fileNameCfg:public		= path.join(sys.cfgdir, 'gurashot.yml')
	//fileNameCfg:public	= 'gurashot.yml'
	fileNameIcon:public		= 'gurashot.ico'
	imgBlank:public = image(`rgba, 800, 800, color(240, 240, 255))
	imgBlank.cairo {|cr|
		cr.set_source_rgb(.8, 0.8, 0.8)
		cr.save {
			cr.scale(1, 1.1)
			cr.arc(400, 250, 180, 0, math.pi * 2)
			cr.fill()
		}
		cr.save {
			cr.scale(1, 5)
			cr.arc(400, 1090, 1000, 0, math.pi * 2)
			cr.fill()
		}
		cr.set_source_rgb(.5, 0.5, 0.5)
		cr.select_font_face('Sans', cairo.FONT_SLANT_NORMAL, cairo.FONT_WEIGHT_NORMAL)
		cr.set_font_size(100.0)
		cr.move_to(210, 400)
		cr.show_text('no image')
	}
	I(src:binary) = image(base64.reader(src))
	imgOpen:public			= I(resource.folder_image_png)
	imgClose:public			= I(resource.delete_png)
	imgShowList:public		= I(resource.report_picture_png)
	imgNext:public			= I(resource.resultset_next_png)
	imgPrev:public			= I(resource.resultset_previous_png)
	imgRotateL:public		= I(resource.arrow_rotate_anticlockwise_png)
	imgRotateR:public		= I(resource.arrow_rotate_clockwise_png)
	imgColorAdjust:public	= I(resource.color_wheel_png)
	imgTiltAdj:public		= I(resource.tilt_adj_png)
	imgCenterGuide:public	= I(resource.center_guide_png)
	imgFrostFrame:public	= I(resource.user_frosted_png)
	imgTick:public			= I(resource.tick_png)
	imgRenameLabel:public	= I(resource.tag_blue_edit_png)
	imgOutputOption:public	= I(resource.page_white_database_png)
	imgOutputPDF:public		= I(resource.page_white_acrobat_png)
	imgLaunchPDFViewer:public = I(resource.page_white_acrobat_png)
	imgPrintService:public	= I(resource.photo_png)
	imgBrightness:public	= I(resource.weather_sun_png)
	imgContrast:public		= I(resource.contrast_png)
	imgBoxRed:public		= I(resource.box_red_png)
	imgBoxGreen:public		= I(resource.box_green_png)
	imgBoxBlue:public		= I(resource.box_blue_png)
	imgColorTone:public		= I(resource.color_swatch_png)
	imgPrintServiceAdjust:public = I(resource.print_service_adjust_png)
	printServiceInfoTbl:public = @(PrintServiceInfo) {
		{ 'Circle K Sunkus'$ +		' | SHARP MX-4500DS',	11, 10,  7, 10 }
		{ 'Three F'$ +				' | SHARP MX-4500DS',	11, 10,  8,  8 }
		{ 'Seven-Eleven Japan'$ +	' | Fuji Xerox',		10,  7,  7,  6 }
		{ 'FamilyMart'$ +			' | SHARP MX-3610DS',	13, 12, 13, 12 }
		{ 'LAWSON'$ +				' | SHARP MX-3610DS',	13, 12, 13, 12 }
	}
	layoutFrameTbl:public = @(LayoutFrame) {
		{ 'Driver\'s license etc.'$,				30, 24, 2, 23, false }
		{ 'Unemployment insurance etc.'$,			30, 25, 2, 23, false }
		{ 'Qualifications'$,						30, 30, 2, 23, false }
		{ 'Qualifications'$,						35, 24, 2, 26, false }
		{ 'Qualifications'$,						36, 24, 2, 27, false }
		{ 'Qualifications'$,						36, 25, 2, 27, false }
		{ 'Resume etc.'$,							40, 30, 2, 30, false }
		{ 'Qualifications'$,						40, 40, 3, 31, false }
		{ 'Passport etc.'$,							45, 35, 4, 34, false }
		{ 'International driver\'s license etc.'$,	50, 40, 4, 38, false }
		{ 'Visa etc.'$,								50, 50, 4, 35, false }
		{ 'Qualifications'$,						55, 40, 2, 41, false }
		{ 'Qualifications'$,						55, 45, 2, 41, false }
		{ 'Qualifications'$,						60, 45, 2, 45, false }
	}
	layoutFrameCur:public		= layoutFrameTbl[0]
	paperSizeTbl:public = @(PaperSize) {
		{ 'L'$,				127,  89, true }
		{ 'KG'$,			152, 102, false }
		{ '2L'$,			178, 127, false }
		{ 'A4'$,			297, 210, false }
		{ 'A3'$,			420, 297, false }
		{ 'Hagaki'$,		148, 100, false }
		{ 'Mutsugiri'$,		254, 203, false }
		{ 'Yotsugiri'$,		305, 254, false }
	}
	cfg:public						= %{}
	imgPhotoRaw:public				= imgBlank
	imgPhotoHighRes:public			= nil
	imgPhotoLowRes:public			= nil
	mainWindow:public				= nil
	xMainWindow:public				= -1
	yMainWindow:public				= -1
	wdMainWindow:public				= 800
	htMainWindow:public				= 730
	wdSashLeft:public				= 320
	htSashTop:public				= 300
	xImageListDialog:public			= -1
	yImageListDialog:public			= -1
	xLayoutFrameEditorDialog:public	= -1
	yLayoutFrameEditorDialog:public	= -1
	xOutputOptionDialog:public		= -1
	yOutputOptionDialog:public		= -1
	xColorAdjustDialog:public		= -1
	yColorAdjustDialog:public		= -1
	sepiaR:public					= 255
	sepiaG:public					= 200
	sepiaB:public					= 150
	printServiceName:public			= ''
	nAdjLines_a:public				= 8
	nAdjLines_b:public				= 8
	nAdjLines_c:public				= 8
	nAdjLines_d:public				= 8
	frostFrameFlag:public			= false
	centerGuideFlag:public			= false
	orientation:public				= `auto		// `auto, `horizontal, `vertical
	trimMark:public					= `outer	// `outer, `overwrite, `none
	trimMarkR:public				= 50
	trimMarkG:public				= 50
	trimMarkB:public				= 255
	titleDispFlag_Size:public		= true
	titleDispFlag_ImageLabel:public	= true
	titleDispFlag_Purpose:public	= true
	paperSizeCur:public				= paperSizeTbl[0]
	imgInfos:public					= []
	imgInfoCur:public				= nil
	wdEdgeMgn:public				= 2	// unit: mm
	htEdgeMgn:public				= 2	// unit: mm
	wdTrimMark:public				= 2	// unit: mm
	htTrimMark:public				= 2	// unit: mm
	htMessage:public				= 5	// unit: mm
	icon:public						= nil
}

//-----------------------------------------------------------------------------
// LoadConfig / SaveConfig
//-----------------------------------------------------------------------------
LoadConfig() = {
	nLayoutFramesUserDef = 5
	try {
		g.cfg = yaml.read(g.fileNameCfg)
		g.xMainWindow:number		= g.cfg['xMainWindow']
		g.yMainWindow:number		= g.cfg['yMainWindow']
		g.wdMainWindow:number		= g.cfg['wdMainWindow']
		g.htMainWindow:number		= g.cfg['htMainWindow']
		g.wdSashLeft:number			= g.cfg['wdSashLeft']
		g.htSashTop:number			= g.cfg['htSashTop']
		g.xImageListDialog:number	= g.cfg['xImageListDialog']
		g.yImageListDialog:number	= g.cfg['yImageListDialog']
		g.xLayoutFrameEditorDialog:number = g.cfg['xLayoutFrameEditorDialog']
		g.yLayoutFrameEditorDialog:number = g.cfg['yLayoutFrameEditorDialog']
		g.xOutputOptionDialog:number= g.cfg['xOutputOptionDialog']
		g.yOutputOptionDialog:number= g.cfg['yOutputOptionDialog']
		g.xColorAdjustDialog:number	= g.cfg['xColorAdjustDialog']
		g.yColorAdjustDialog:number	= g.cfg['yColorAdjustDialog']
		g.sepiaR:number				= g.cfg['sepiaR']
		g.sepiaG:number				= g.cfg['sepiaG']
		g.sepiaB:number				= g.cfg['sepiaB']
		g.printServiceName			= g.cfg['printServiceName']
		g.nAdjLines_a:number		= g.cfg['nAdjLines_a']
		g.nAdjLines_b:number		= g.cfg['nAdjLines_b']
		g.nAdjLines_c:number		= g.cfg['nAdjLines_c']
		g.nAdjLines_d:number		= g.cfg['nAdjLines_d']
		g.frostFrameFlag			= (g.cfg['frostFrameFlag'] == 'true')
		g.centerGuideFlag			= (g.cfg['centerGuideFlag'] == 'true')
		g.orientation				= [`horizontal, `vertical, `auto].\
							find(g.cfg['orientation'].tosymbol()) || `auto
		g.trimMark					= [`outer, `overwrite, `none].\
							find(g.cfg['trimMark'].tosymbol()) || `outer
		g.trimMarkR:number			= g.cfg['trimMarkR']
		g.trimMarkG:number			= g.cfg['trimMarkG']
		g.trimMarkB:number			= g.cfg['trimMarkB']
		g.titleDispFlag_Size		= (g.cfg['titleDispFlag_Size'] == 'true')
		g.titleDispFlag_ImageLabel	= (g.cfg['titleDispFlag_ImageLabel'] == 'true')
		g.titleDispFlag_Purpose		= (g.cfg['titleDispFlag_Purpose'] == 'true')
		g.wdEdgeMgn:number			= g.cfg['wdEdgeMgn']
		g.htEdgeMgn:number			= g.cfg['htEdgeMgn']
		idxPaperSizeCur:number	= g.cfg['idxPaperSizeCur'].tonumber()
		g.paperSizeCur			= g.paperSizeTbl[idxPaperSizeCur]
		g.cfg['layoutFramesUserDef'].each():list {|dict|
			g.layoutFrameTbl.add(LayoutFrame(dict['purpose'],
					dict['htFrame'], dict['wdFrame'],
					dict['htUpperMgn'], dict['htFacePart'], true))
			nLayoutFramesUserDef -= 1
		}
		g.imgInfos = g.cfg['imgInfos'].each():list {|dict|
			imgInfo = ImageInfo()
			imgInfo.label				= dict['label']
			imgInfo.fileName			= dict['fileName']
			imgInfo.rotate:number		= dict['rotate']
			imgInfo.xrateTop:number		= dict['xrateTop']
			imgInfo.xrateBtm:number		= dict['xrateBtm']
			imgInfo.yrateTop:number		= dict['yrateTop']
			imgInfo.yrateBtm:number		= dict['yrateBtm']
			imgInfo.tiltAdjFlag			= (dict['tiltAdjFlag'] == 'true')
			imgInfo.colorTone			= [`color, `mono, `sepia].\
							find(dict['colorTone'].tosymbol()) || `color
			imgInfo.brightness:number	= dict['brightness']
			imgInfo.contrast:number		= dict['contrast']
			imgInfo.redness:number		= dict['redness']
			imgInfo.greenness:number	= dict['greenness']
			imgInfo.blueness:number		= dict['blueness']
			imgInfo
		}
	} catch {}
	repeat (nLayoutFramesUserDef) {
		g.layoutFrameTbl.add(LayoutFrame('', 40, 30, 2, 30, true))
	}
	if (g.imgInfos.isempty()) {
		g.imgInfoCur = ImageInfo()
	} else {
		try {
			idxImgInfoCur = g.cfg['idxImgInfoCur'].tonumber()
			g.imgInfoCur = g.imgInfos[idxImgInfoCur]
		} catch {|e|
			g.imgInfoCur = g.imgInfos.last()
		}
	}
	if (!g.imgInfoCur.fileName.isempty()) {
		try {
			g.imgPhotoRaw = image(g.imgInfoCur.fileName)
		} catch {
			g.imgInfoCur.fileName = ''
		}
	}
}

SaveConfig() = {
	g.cfg['xMainWindow']				= g.xMainWindow
	g.cfg['yMainWindow']				= g.yMainWindow
	g.cfg['wdMainWindow']				= g.wdMainWindow
	g.cfg['htMainWindow']				= g.htMainWindow
	g.cfg['wdSashLeft']					= g.wdSashLeft
	g.cfg['htSashTop']					= g.htSashTop
	g.cfg['xImageListDialog']			= g.xImageListDialog
	g.cfg['yImageListDialog']			= g.yImageListDialog
	g.cfg['xLayoutFrameEditorDialog']	= g.xLayoutFrameEditorDialog
	g.cfg['yLayoutFrameEditorDialog']	= g.yLayoutFrameEditorDialog
	g.cfg['xOutputOptionDialog']		= g.xOutputOptionDialog
	g.cfg['yOutputOptionDialog']		= g.yOutputOptionDialog
	g.cfg['xColorAdjustDialog']			= g.xColorAdjustDialog
	g.cfg['yColorAdjustDialog']			= g.yColorAdjustDialog
	g.cfg['sepiaR']						= g.sepiaR
	g.cfg['sepiaG']						= g.sepiaG
	g.cfg['sepiaB']						= g.sepiaB
	g.cfg['printServiceName']			= g.printServiceName
	g.cfg['nAdjLines_a']				= g.nAdjLines_a
	g.cfg['nAdjLines_b']				= g.nAdjLines_b
	g.cfg['nAdjLines_c']				= g.nAdjLines_c
	g.cfg['nAdjLines_d']				= g.nAdjLines_d
	g.cfg['frostFrameFlag']				= g.frostFrameFlag
	g.cfg['centerGuideFlag']			= g.centerGuideFlag
	g.cfg['orientation']				= g.orientation
	g.cfg['trimMark']					= g.trimMark
	g.cfg['trimMarkR']					= g.trimMarkR
	g.cfg['trimMarkG']					= g.trimMarkG
	g.cfg['trimMarkB']					= g.trimMarkB
	g.cfg['titleDispFlag_Size']			= g.titleDispFlag_Size
	g.cfg['titleDispFlag_ImageLabel']	= g.titleDispFlag_ImageLabel
	g.cfg['titleDispFlag_Purpose']		= g.titleDispFlag_Purpose
	g.cfg['wdEdgeMgn']					= g.wdEdgeMgn
	g.cfg['htEdgeMgn']					= g.htEdgeMgn
	g.cfg['idxPaperSizeCur']			= g.paperSizeTbl.find(g.paperSizeCur):index || 0
	g.cfg['layoutFramesUserDef'] = g.layoutFrameTbl.each():xlist {|layoutFrame|
		!layoutFrame.userDefFlag && continue
		%{
			'purpose'		=> layoutFrame.purpose
			'htFrame'		=> layoutFrame.htFrame
			'wdFrame'		=> layoutFrame.wdFrame
			'htUpperMgn'	=> layoutFrame.htUpperMgn
			'htFacePart'	=> layoutFrame.htFacePart
		}
	}
	g.cfg['idxImgInfoCur']				= g.imgInfos.find(g.imgInfoCur):index || -1
	g.cfg['imgInfos'] = g.imgInfos.each():list {|imgInfo|
		%{
			'label'			=> imgInfo.label
			'fileName'		=> imgInfo.fileName
			'rotate'		=> imgInfo.rotate
			'xrateTop'		=> imgInfo.xrateTop
			'xrateBtm'		=> imgInfo.xrateBtm
			'yrateTop'		=> imgInfo.yrateTop
			'yrateBtm'		=> imgInfo.yrateBtm
			'tiltAdjFlag'	=> imgInfo.tiltAdjFlag
			'colorTone'		=> imgInfo.colorTone
			'brightness'	=> imgInfo.brightness
			'contrast'		=> imgInfo.contrast
			'redness'		=> imgInfo.redness
			'greenness'		=> imgInfo.greenness
			'blueness'		=> imgInfo.blueness
		}
	}
	yaml.write(g.fileNameCfg, g.cfg)
}

InitializeResource() = {
	g.icon = wx.Icon(g.fileNameIcon, wx.BITMAP_TYPE_ICO)
}

//-----------------------------------------------------------------------------
// UpdateImagePhoto
//-----------------------------------------------------------------------------
UpdateImagePhoto() = {
	g.imgPhotoHighRes = g.imgPhotoRaw.rotate(g.imgInfoCur.rotate)
	g.imgPhotoLowRes = g.imgPhotoHighRes.thumbnail(1000):box
}

//-----------------------------------------------------------------------------
// SetBlankImage
//-----------------------------------------------------------------------------
SetBlankImage() = {
	g.imgInfoCur = ImageInfo()
	g.imgPhotoRaw = g.imgBlank
	UpdateImagePhoto()
}

//-----------------------------------------------------------------------------
// ReadImageFile
//-----------------------------------------------------------------------------
ReadImageFile(parent:wx.Window, fileName:string) = {
	idx = g.imgInfos:*fileName.find(fileName):index
	if (idx) {
		g.imgInfoCur = g.imgInfos[idx]
	} else {
		g.imgInfoCur = ImageInfo()
		g.imgInfoCur.label = path.basename(path.filename(fileName))
		g.imgInfoCur.fileName = fileName
		g.imgInfos.add(g.imgInfoCur)
	}
	if (g.imgInfoCur.imgPhotoRaw) {
		g.imgPhotoRaw = g.imgInfoCur.imgPhotoRaw
	} else {
		try {
			imgPhotoRaw = image(fileName)
		} catch {
			wx.MessageDialog(parent, 'Failed to read the image file'$,
							'Error'$, wx.OK | wx.ICON_ERROR | wx.CENTRE) {|dlg|
				dlg.ShowModal()
			}
			return
		}
		g.imgPhotoRaw = g.imgInfoCur.imgPhotoRaw = imgPhotoRaw
	}
	UpdateImagePhoto()
}

//-----------------------------------------------------------------------------
// RotateImageL / RotateImageR
//-----------------------------------------------------------------------------
RotateImageL() = {
	g.imgInfoCur.rotate = (g.imgInfoCur.rotate + 270) % 360
	UpdateImagePhoto()
}

RotateImageR() = {
	g.imgInfoCur.rotate = (g.imgInfoCur.rotate + 90) % 360
	UpdateImagePhoto()
}

//-----------------------------------------------------------------------------
// OpenWithApp
//-----------------------------------------------------------------------------
OpenWithApp(fileName:string) = {
	mswin.ole('Wscript.shell') {|wsh| wsh.Run(fileName)}
}

//-----------------------------------------------------------------------------
// ExtractImage
//-----------------------------------------------------------------------------
ExtractImage(imgPhoto:image, layoutFrame:LayoutFrame, wdEdgeMgn:number, htEdgeMgn:number) = {
	bgColor = `gray
	[wdDummy, htDummy] = int([imgPhoto.width / 2, imgPhoto.height / 2])
	imgSrc = image(`rgba, imgPhoto.width + wdDummy * 2, imgPhoto.height + htDummy * 2, bgColor)
	// +----+----+----+
	// | NW | N  | NE |
	// +----+----+----+
	// | W  | c  | E  |
	// +----+----+----+
	// | SW | S  | SE |
	// +----+----+----+
	imgSrc.paste(wdDummy, htDummy, imgPhoto)
	// paste dummy on N
	imgSrc.paste(wdDummy, 0,
		imgPhoto.crop(0, 0, imgPhoto.width, htDummy).flip(`vert))
	// paste dummy on S
	imgSrc.paste(wdDummy, htDummy + imgPhoto.height,
		imgPhoto.crop(0, imgPhoto.height - htDummy, imgPhoto.width, htDummy).flip(`vert))
	// paste dummy on W
	imgSrc.paste(0, htDummy,
		imgPhoto.crop(0, 0, wdDummy, imgPhoto.height).flip(`horz))
	// paste dummy on E
	imgSrc.paste(wdDummy + imgPhoto.width, htDummy,
		imgPhoto.crop(imgPhoto.width - wdDummy, 0, wdDummy, imgPhoto.height).flip(`horz))
	// paste dummy on NW
	imgSrc.paste(0, 0,
		imgPhoto.crop(0, 0, wdDummy, htDummy).rotate(180))
	// paste dummy on NE
	imgSrc.paste(wdDummy + imgPhoto.width, 0,
		imgPhoto.crop(imgPhoto.width - wdDummy, 0, wdDummy, htDummy).rotate(180))
	// paste dummy on SW
	imgSrc.paste(0, htDummy + imgPhoto.height,
		imgPhoto.crop(0, imgPhoto.height - htDummy, wdDummy, htDummy).rotate(180))
	// paste dummy on SE
	imgSrc.paste(wdDummy + imgPhoto.width, htDummy + imgPhoto.height,
		imgPhoto.crop(imgPhoto.width - wdDummy, imgPhoto.height - htDummy, wdDummy, htDummy).rotate(180))
	f = g.imgInfoCur.CalcFrameCoord(wx.Rect(wdDummy, htDummy, imgPhoto.width, imgPhoto.height), layoutFrame)
	[wdDotMgn, htDotMgn] = f.height * [wdEdgeMgn, htEdgeMgn] / layoutFrame.htFrame
	if (g.imgInfoCur.tiltAdjFlag) {
		angleAdj = math.atan2(f.xBtm - f.xTop, f.yBtm - f.yTop):deg
		[xCenter, yCenter] = [imgSrc.width / 2, imgSrc.height / 2]
		imgSrc = imgSrc.rotate(angleAdj, `gray)
		[xPivot, yPivot] = [f.xTop - xCenter, -f.yTop + yCenter]
		[yPivot, xPivot] = matrix.rotation(angleAdj):deg * [yPivot, xPivot]
		[xPivot, yPivot] = [xPivot + imgSrc.width / 2, -yPivot + imgSrc.height / 2]
	} else {
		[xPivot, yPivot] = [f.xTop, f.yTop]
	}
	[xCrop, yCrop] = [xPivot - f.width / 2 - wdDotMgn, yPivot - htDotMgn]
	[wdCrop, htCrop] = [f.width + wdDotMgn * 2, f.height + htDotMgn * 2]
	imgSrc.crop(xCrop, yCrop, wdCrop, htCrop)
}

//-----------------------------------------------------------------------------
// ComposeProduct
//-----------------------------------------------------------------------------
ComposeProduct(cr:cairo.context, imgPhoto:image, layoutFrame:LayoutFrame,
								paperSize:PaperSize, orientation:symbol) = {
	fontName = 'Sans'
	[wdCanvas, htCanvas] = [paperSize.GetCanvasWidth(orientation), paperSize.GetCanvasHeight(orientation)]
	scope {
		cr.select_font_face(fontName, cairo.FONT_SLANT_NORMAL, cairo.FONT_WEIGHT_NORMAL)
		cr.set_source_rgb(.2, .2, 1)
		cr.set_font_size(3.0)
		cr.move_to(0, htCanvas - 1)
		text = ''
		if (g.titleDispFlag_Size) {
			text += format('[%d x %dmm]', layoutFrame.htFrame, layoutFrame.wdFrame)
		}
		if (g.titleDispFlag_ImageLabel && !g.imgInfoCur.label.isempty()) {
			if (!text.isempty()) { text += ' ' }
			text += g.imgInfoCur.label
		}
		if (g.titleDispFlag_Purpose && !layoutFrame.purpose.isempty()) {
			if (!text.isempty()) { text += ' ' }
			text += format('for %s', layoutFrame.purpose)
		}
		cr.show_text(text)
	}
	imgExtract = g.imgInfoCur.AdjustColor(ExtractImage(imgPhoto,
								layoutFrame, g.wdEdgeMgn, g.htEdgeMgn).thumbnail(1000):box)
	cairo.pattern.create_for_surface(imgExtract) {|pattern|
		cr.set_line_width(.1)
		cr.set_source_rgb(([g.trimMarkR, g.trimMarkG, g.trimMarkB] / 255)*)
		[wdFrameEx, htFrameEx] = layoutFrame.CalcSizeEx()
		[nx, ny] = layoutFrame.CalcImageGrid(wdCanvas, htCanvas)
		wdSlot = wdCanvas / nx
		htSlot = (htCanvas - g.htMessage) / ny
		drawTrimMarks() = {
			repeat(nx) {|ix|
				xSlot = wdSlot * ix
				x = xSlot + (wdSlot - wdFrameEx) / 2
				cr.move_to(x + g.wdEdgeMgn, 0)
				cr.line_to(x + g.wdEdgeMgn, htCanvas - g.htMessage)
				cr.move_to(x + wdFrameEx - g.wdEdgeMgn, 0)
				cr.line_to(x + wdFrameEx - g.wdEdgeMgn, htCanvas - g.htMessage)
				cr.stroke()
			}
			repeat(ny) {|iy|
				ySlot = htSlot * iy
				y = ySlot + (htSlot - htFrameEx) / 2
				cr.move_to(0, y + g.htEdgeMgn)
				cr.line_to(wdCanvas, y + g.htEdgeMgn)
				cr.move_to(0, y + htFrameEx - g.htEdgeMgn)
				cr.line_to(wdCanvas, y + htFrameEx - g.htEdgeMgn)
				cr.stroke()
			}
		}
		drawFaceImages() = {
			cross(ix in range(nx), iy in range(ny)) {
				xSlot = wdSlot * ix
				ySlot = htSlot * iy
				x = xSlot + (wdSlot - wdFrameEx) / 2
				y = ySlot + (htSlot - htFrameEx) / 2
				cr.save {
					scale = htFrameEx / imgExtract.height
					cr.translate(x, y)
					cr.scale(scale, scale)
					cr.set_source(pattern)
					cr.paint()
				}
			}
		}
		if (g.trimMark == `outer) {
			drawTrimMarks()
			drawFaceImages()
		} elsif (g.trimMark == `overwrite) {
			drawFaceImages()
			drawTrimMarks()
		} else {	// g.trimMark == `none
			drawFaceImages()
		}
	}
}

//-----------------------------------------------------------------------------
// ComposeProductPDF
//-----------------------------------------------------------------------------
ComposeProductPDF(fileName:string, imgPhoto:image, layoutFrame:LayoutFrame,
									paperSize:PaperSize, orientation:symbol) = {
	cairo.create(cairo.pdf_surface.create(fileName,
					units.mm$pt(paperSize.GetCanvasWidth(orientation)),
					units.mm$pt(paperSize.GetCanvasHeight(orientation)))) {|cr|
		scale = 72 / 25.4
		cr.scale(scale, scale)
		ComposeProduct(cr, imgPhoto, layoutFrame, paperSize, orientation)
		cr.show_page()
	}
}

//-----------------------------------------------------------------------------
// DrawAdjustLines
//-----------------------------------------------------------------------------
DrawAdjustLines(img:image, nAdjLinesDrawn:number, printServiceName:string,
		nAdjLines_a:number, nAdjLines_b:number, nAdjLines_c:number, nAdjLines_d:number) = {
	fontName = 'Sans'
	[width, height] = [img.width, img.height]
	img.cairo {|cr|
		cr.set_source_rgb(.3, 1, .3)
		repeat (nAdjLinesDrawn) {|i|
			if ((i + 1) % 10 == 0) {
				cr.set_line_width(4)
			} elsif ((i + 1) % 5 == 0) {
				cr.set_line_width(2)
			} else {
				cr.set_line_width(1)
			}
			y = (nAdjLinesDrawn - i) * 10
			cr.move_to(0, y)
			cr.line_to(width, y)
			y = height - (nAdjLinesDrawn - i) * 10
			cr.move_to(0, y)
			cr.line_to(width, y)
			x = (nAdjLinesDrawn - i) * 10
			cr.move_to(x, 0)
			cr.line_to(x, height)
			x = width - (nAdjLinesDrawn - i) * 10
			cr.move_to(x, 0)
			cr.line_to(x, height)
			cr.stroke()
		}
		cr.set_source_rgb(0, .7, 0)
		cr.select_font_face(fontName, cairo.FONT_SLANT_NORMAL, cairo.FONT_WEIGHT_NORMAL)
		cr.set_font_size(50)
		if (!printServiceName.isempty()) {
			fields = printServiceName.split('|'):list
			text = fields[0].strip()
			if (fields.len() > 1) {
				text += ' [' + fields[1].strip() + ']'
			}
			x = nAdjLinesDrawn * 10 + 10
			y = nAdjLinesDrawn * 10 - 10
			cr.move_to(x, y)
			cr.show_text(text)
		}
		scope {
			text = format('a = %d, b = %d, c = %d, d = %d',
								nAdjLines_a, nAdjLines_b, nAdjLines_c, nAdjLines_d)
			extText = cr.text_extents(text)
			extFont = cr.font_extents()
			x = width - nAdjLinesDrawn * 10 - extText.x_advance - 10
			y = nAdjLinesDrawn * 10 - 10
			cr.move_to(x, y)
			cr.show_text(text)
		}
	}
}

//-----------------------------------------------------------------------------
// ComposeProductForPrintService
//-----------------------------------------------------------------------------
ComposeProductForPrintService(fileName:string, imgPhoto:image, layoutFrame:LayoutFrame,
		paperSize:PaperSize, orientation:symbol, dpi:number,
		nAdjLinesDrawn:number, printServiceName:string,
		nAdjLines_a:number, nAdjLines_b:number, nAdjLines_c:number, nAdjLines_d:number) = {
	nAdjLinesLongSide = nAdjLines_c + nAdjLines_d
	nAdjLinesShortSide = nAdjLines_a + nAdjLines_b
	longSideDrop = (nAdjLinesDrawn * 2 - nAdjLinesLongSide) * 10
	shortSideDrop = (nAdjLinesDrawn * 2 - nAdjLinesShortSide) * 10
	[longSideOuter, shortSideOuter] = int([paperSize.longSide, paperSize.shortSide] * dpi / 25.4)
	longSideInner = longSideOuter - longSideDrop
	shortSideInner = shortSideOuter - shortSideDrop
	longSideCanvas = longSideInner * (paperSize.longSide - paperSize.margin * 2) / paperSize.longSide
	shortSideCanvas = shortSideInner * (paperSize.shortSide - paperSize.margin * 2) / paperSize.shortSide
	if (orientation == `horizontal) {
		[wdOuter, htOuter] = [longSideOuter, shortSideOuter]
		[wdInner, htInner] = [longSideInner, shortSideInner]
		[wdCanvas, htCanvas] = [longSideCanvas, shortSideCanvas]
	} else { // orientation == `vertical
		[wdOuter, htOuter] = [shortSideOuter, longSideOuter]
		[wdInner, htInner] = [shortSideInner, longSideInner]
		[wdCanvas, htCanvas] = [shortSideCanvas, longSideCanvas]
	}
	img = image(`rgba, wdOuter, htOuter, `white)
	DrawAdjustLines(img, nAdjLinesDrawn, printServiceName,
				nAdjLines_a, nAdjLines_b, nAdjLines_c, nAdjLines_d)
	img.cairo {|cr|
		cr.translate((wdOuter - wdCanvas) / 2, (htOuter - htCanvas) / 2)
		scale = wdInner / wdOuter * dpi / 25.4
		cr.scale(scale, scale)
		ComposeProduct(cr, imgPhoto, layoutFrame, paperSize, orientation)
		cr.show_page()
	}
	img.write(fileName)
}

//-----------------------------------------------------------------------------
// ProductViewer
//-----------------------------------------------------------------------------
ProductViewer = class(wx.Panel) {
	// constructor
	__init__(parent:wx.Windowm, style:number) = {|parent, style => (style | wx.HSCROLL | wx.VSCROLL)|
		this.bmpScreen = nil
		this.brushBg = wx.Brush(wx.Colour(240, 240, 240), wx.BRUSHSTYLE_SOLID)
		// bind event handlers
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_SCROLLWIN) {|event| this.OnScrollWin(event)}
		this.Bind(wx.EVT_MOUSEWHEEL) {|event| this.OnMouseWheel(event)}
	}
	// methods
	UpdateContent(blankFlag:boolean => false) = {
		[wdScreenMgn, htScreenMgn] = [8, 8]
		orientation = g.paperSizeCur.GetBetterOrientation(g.layoutFrameCur, g.orientation)
		wdCanvas = g.paperSizeCur.GetCanvasWidth(orientation)
		htCanvas = g.paperSizeCur.GetCanvasHeight(orientation)
		if (wdCanvas < htCanvas) {
			wdImg = 400, htImg = int(wdImg * htCanvas / wdCanvas)
		} else {
			htImg = 400, wdImg = int(htImg * wdCanvas / htCanvas)
		}
		img = image(`rgba, wdImg, htImg, `white)
		if (!blankFlag) {
			img.cairo {|cr|
				scale = wdImg / wdCanvas
				cr.scale(scale, scale)
				ComposeProduct(cr, g.imgPhotoLowRes,
								g.layoutFrameCur, g.paperSizeCur, orientation)
			}
		}
		[wdClient, htClient] = this.GetClientSizeWH()
		wdScreen = max(wdClient, img.width + wdScreenMgn * 2)
		htScreen = max(htClient, img.height + htScreenMgn * 2)
		this.bmpScreen = wx.BitmapWH(wdScreen, htScreen)
		wx.MemoryDC(this.bmpScreen) {|dc|
			dc.SetBackground(this.brushBg)
			dc.Clear()
			[x, y] = [(wdScreen - img.width) / 2, (htScreen - img.height) / 2]
			dc.DrawBitmap(img, x, y, false)
		}
		x = this.GetScrollPos(wx.HORIZONTAL)
		y = this.GetScrollPos(wx.VERTICAL)
		this.SetScrollbar(wx.HORIZONTAL, x, wdClient, wdScreen)
		this.SetScrollbar(wx.VERTICAL, y, htClient, htScreen)
		this.Refresh()
		this.Update()
	}
	// event handler
	OnSize(event:wx.SizeEvent) = {
		this.UpdateContent()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		x = this.GetScrollPos(wx.HORIZONTAL)
		y = this.GetScrollPos(wx.VERTICAL)
		this.bmpScreen && dc.DrawBitmap(this.bmpScreen, -x, -y, false)
		dc = nil
	}
	OnScrollWin(event:wx.ScrollWinEvent) = {
		eventType = event.GetEventType()
		orientation = event.GetOrientation()
		if (eventType == wx.EVT_SCROLLWIN_TOP.GetEventType()) {
			pos = 0
		} elsif (eventType == wx.EVT_SCROLLWIN_BOTTOM.GetEventType()) {
			pos = this.GetScrollRange(orientation)
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 10
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 10
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 100
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 100
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBTRACK.GetEventType()) {
			pos = event.GetPosition()
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBRELEASE.GetEventType()) {
			pos = event.GetPosition()
		}
		this.SetScrollPos(orientation, pos)
		this.Refresh()
	}
	OnMouseWheel(event:wx.MouseEvent) = {
		orientation = wx.VERTICAL
		rot = event.GetWheelRotation()
		pos = this.GetScrollPos(orientation)
		if (rot < 0) {
			this.SetScrollPos(orientation, pos + 10)
			this.Refresh()
		} elsif (rot > 0) {
			this.SetScrollPos(orientation, pos - 10)
			this.Refresh()
		}
	}
}

//-----------------------------------------------------------------------------
// Extractor
//-----------------------------------------------------------------------------
Extractor = class(wx.Panel) {
	// constructor
	__init__(parent:wx.Window, style:number, productViewer:ProductViewer) = \
													{|parent, style => style|
		this.productViewer = productViewer
		this.grabTgt = nil
		this.bmpScreen = nil
		this.rcImg = wx.Rect(0, 0, 0, 0)
		this.penCursor = wx.Pen(wx.Colour(255, 128, 128), 2, wx.PENSTYLE_SOLID)
		this.penCenterGuide = wx.Pen(wx.Colour(255, 128, 128), 1, wx.PENSTYLE_SOLID)
		this.brushBg = wx.Brush(wx.Colour(128, 128, 128), wx.BRUSHSTYLE_SOLID)
		this.brushCursor = wx.Brush(wx.Colour(255, 255, 255), wx.BRUSHSTYLE_SOLID)
		this.DragAcceptFiles(true)
		this.mapTblFrosted = interval(64, 140, 256):list
		// bind event handlers
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_LEFT_DOWN) {|event| this.OnLeftDown(event)}
		this.Bind(wx.EVT_LEFT_UP) {|event| this.OnLeftUp(event)}
		this.Bind(wx.EVT_MOUSE_CAPTURE_LOST) {|event| this.OnMouseCaptureLost(event)}
		this.Bind(wx.EVT_MOTION) {|event| this.OnMotion(event)}
		this.Bind(wx.EVT_DROP_FILES) {|event| this.OnDropFiles(event)}
	}
	// methods
	SenseBox(pt:wx.Point) = {
		f = g.imgInfoCur.CalcFrameCoord(this.rcImg, g.layoutFrameCur)
		if (g.imgInfoCur.tiltAdjFlag) {
			[wdSense, htSense] = [6, 6]
			wx.Rect(f.xMarkTop - wdSense / 2, f.yMarkTop - htSense / 2, wdSense, htSense).Contains(pt) && return (`topPoint)
			wx.Rect(f.xMarkBtm - wdSense / 2, f.yMarkBtm - htSense / 2, wdSense, htSense).Contains(pt) && return (`btmPoint)
		} else {
			[wdSense, htSense] = [6, 6]
			wx.Rect(f.xMarkTop - f.width / 4, f.yMarkTop - htSense / 2, f.width, htSense).Contains(pt) && return(`topLine)
			wx.Rect(f.xMarkTop - f.width / 4, f.yMarkBtm - htSense / 2, f.width, htSense).Contains(pt) && return(`btmLine)
			wx.Rect(f.xMarkTop - wdSense / 2, f.yMarkTop, wdSense, f.yMarkBtm - f.yMarkTop).Contains(pt) && return(`center)
		}
		nil
	}
	UpdateContent(blankFlag:boolean => false) = {
		if (blankFlag) {
			this.BlankImageScreen()
		} else {
			this.UpdateImageScreen()
		}
		this.Refresh()
		this.Update()
	}
	BlankImageScreen() = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.bmpScreen = wx.BitmapWH(wdClient, htClient)
		wx.MemoryDC(this.bmpScreen) {|dc|
			dc.SetBackground(this.brushBg)
			dc.Clear()
		}
	}
	UpdateImageScreen() = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.bmpScreen = wx.BitmapWH(wdClient, htClient)
		img = g.imgInfoCur.AdjustColor(g.imgPhotoLowRes.thumbnail(wdClient, htClient))
		imgFrosted = img.mapcolorlevel(this.mapTblFrosted)
		[xOffset, yOffset] = [(wdClient - img.width) / 2, (htClient - img.height) / 2]
		this.rcImg = wx.Rect(xOffset, yOffset, img.width, img.height)
		wx.MemoryDC(this.bmpScreen) {|dc|
			dc.SetBackground(this.brushBg)
			dc.Clear()
			if (g.frostFrameFlag) {
				dc.DrawBitmap(imgFrosted, xOffset, yOffset, false)
				f = g.imgInfoCur.CalcFrameCoord(this.rcImg, g.layoutFrameCur)
				angle = math.atan2(f.yBtm - f.yTop, f.xBtm - f.xTop)
				[xOffset1, yOffset1, xOffset2, yOffset2] = [
					math.cos(angle - math.pi / 2)
					math.sin(angle - math.pi / 2)
					math.cos(angle + math.pi / 2)
					math.sin(angle + math.pi / 2)
				] * f.width / 2
				ptTbl = @(wx.Point) {
					{f.xTop + xOffset1, f.yTop + yOffset1},
					{f.xTop + xOffset2, f.yTop + yOffset2},
					{f.xBtm + xOffset2, f.yBtm + yOffset2},
					{f.xBtm + xOffset1, f.yBtm + yOffset1},
				}
				dc.SetClippingRegionAsRegion(wx.RegionPoints(ptTbl))
			}
			dc.DrawBitmap(img, xOffset, yOffset, false)
		}
	}
	// event handler
	OnSize(event:wx.SizeEvent) = {
		this.UpdateContent()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		this.UpdateImageScreen()
		dc.DrawBitmap(this.bmpScreen, 0, 0, false)
		f = g.imgInfoCur.CalcFrameCoord(this.rcImg, g.layoutFrameCur)
		dc.SetPen(this.penCursor)
		dc.SetBrush(this.brushCursor)
		angle = math.atan2(f.yMarkBtm - f.yMarkTop, f.xMarkBtm - f.xMarkTop)
		[xOffset1, yOffset1, xOffset2, yOffset2] = [
			math.cos(angle - math.pi / 2)
			math.sin(angle - math.pi / 2)
			math.cos(angle + math.pi / 2)
			math.sin(angle + math.pi / 2)
		] * f.width / 4
		[xCenter, yCenter] = [(f.xMarkTop + f.xMarkBtm) / 2, (f.yMarkTop + f.yMarkBtm) / 2]
		dc.DrawLine(f.xMarkTop, f.yMarkTop, f.xMarkBtm, f.yMarkBtm)
		dc.DrawLine(f.xMarkTop + xOffset1, f.yMarkTop + yOffset1, f.xMarkTop + xOffset2, f.yMarkTop + yOffset2)
		dc.DrawLine(f.xMarkBtm + xOffset1, f.yMarkBtm + yOffset1, f.xMarkBtm + xOffset2, f.yMarkBtm + yOffset2)
		if (g.centerGuideFlag) {
			[xOffset1, yOffset1, xOffset2, yOffset2] = [
				math.cos(angle - math.pi / 2)
				math.sin(angle - math.pi / 2)
				math.cos(angle + math.pi / 2)
				math.sin(angle + math.pi / 2)
			] * f.width / 3
			dc.SetPen(this.penCenterGuide)
			dc.DrawLine(xCenter + xOffset1, yCenter + yOffset1, xCenter + xOffset2, yCenter + yOffset2)
		}
		if (g.imgInfoCur.tiltAdjFlag) {
			dc.DrawCircle(f.xMarkTop, f.yMarkTop, 4)
			dc.DrawCircle(f.xMarkBtm, f.yMarkBtm, 4)
		}
		dc = nil
	}
	OnLeftDown(event:wx.MouseEvent) = {
		if (!this.grabTgt) {
			this.grabTgt = this.SenseBox(event.GetPosition())
		}
		!this.HasCapture() && this.CaptureMouse()
	}
	OnLeftUp(event:wx.MouseEvent) = {
		this.HasCapture() && this.ReleaseMouse()
		if (this.grabTgt) {
			this.grabTgt = nil
			wx.BusyCursor {
				this.productViewer.UpdateContent(true)
				this.productViewer.UpdateContent()
			}
		}
	}
	OnMouseCaptureLost(event:wx.MouseCaptureLost) = {
		if (this.grabTgt) {
			this.grabTgt = nil
		}
	}
	OnMotion(event:wx.MouseEvent) = {
		rtn = this.SenseBox(event.GetPosition())
		if (rtn == `center) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_SIZEWE))
		} elsif (rtn in [`topLine, `btmLine]) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_SIZENS))
		} elsif (rtn in [`topPoint, `btmPoint]) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_CROSS))
		} else {
			this.SetCursor(wx.StockCursor(wx.CURSOR_ARROW))
		}
		pt = event.GetPosition()
		if (this.grabTgt == `center) {
			g.imgInfoCur.xrateTop = (pt.x - this.rcImg.x) / this.rcImg.width
			if (g.imgInfoCur.xrateTop > 1) {
				g.imgInfoCur.xrateTop = 1
			} elsif (g.imgInfoCur.xrateTop < 0) {
				g.imgInfoCur.xrateTop = 0
			}
			g.imgInfoCur.xrateBtm = g.imgInfoCur.xrateTop
			this.Refresh()
		} elsif (this.grabTgt == `topLine) {
			g.imgInfoCur.yrateTop = (pt.y - this.rcImg.y) / this.rcImg.height
			if (g.imgInfoCur.yrateTop < 0) {
				g.imgInfoCur.yrateTop = 0
			} elsif (g.imgInfoCur.yrateTop > g.imgInfoCur.yrateBtm - .1) {
				g.imgInfoCur.yrateTop = g.imgInfoCur.yrateBtm - .1
			}
			this.Refresh()
		} elsif (this.grabTgt == `btmLine) {
			g.imgInfoCur.yrateBtm = (pt.y - this.rcImg.y) / this.rcImg.height
			if (g.imgInfoCur.yrateBtm > 1) {
				g.imgInfoCur.yrateBtm = 1
			} elsif (g.imgInfoCur.yrateBtm < g.imgInfoCur.yrateTop + .1) {
				g.imgInfoCur.yrateBtm = g.imgInfoCur.yrateTop + .1
			}
			this.Refresh()
		} elsif (this.grabTgt == `topPoint) {
			g.imgInfoCur.xrateTop = (pt.x - this.rcImg.x) / this.rcImg.width
			g.imgInfoCur.yrateTop = (pt.y - this.rcImg.y) / this.rcImg.height
			if (g.imgInfoCur.xrateTop > 1) {
				g.imgInfoCur.xrateTop = 1
			} elsif (g.imgInfoCur.xrateTop < 0) {
				g.imgInfoCur.xrateTop = 0
			}
			if (g.imgInfoCur.yrateTop < 0) {
				g.imgInfoCur.yrateTop = 0
			} elsif (g.imgInfoCur.yrateTop > g.imgInfoCur.yrateBtm - .1) {
				g.imgInfoCur.yrateTop = g.imgInfoCur.yrateBtm - .1
			}
			this.Refresh()
		} elsif (this.grabTgt == `btmPoint) {
			g.imgInfoCur.xrateBtm = (pt.x - this.rcImg.x) / this.rcImg.width
			g.imgInfoCur.yrateBtm = (pt.y - this.rcImg.y) / this.rcImg.height
			if (g.imgInfoCur.xrateBtm > 1) {
				g.imgInfoCur.xrateBtm = 1
			} elsif (g.imgInfoCur.xrateBtm < 0) {
				g.imgInfoCur.xrateBtm = 0
			}
			if (g.imgInfoCur.yrateBtm > 1) {
				g.imgInfoCur.yrateBtm = 1
			} elsif (g.imgInfoCur.yrateBtm < g.imgInfoCur.yrateTop + .1) {
				g.imgInfoCur.yrateBtm = g.imgInfoCur.yrateTop + .1
			}
			this.Refresh()
		}
	}
	OnDropFiles(event:wx.DropFilesEvent) = {
		fileNames = event.GetFiles()
		ReadImageFile(this, fileNames[0])
		this.UpdateContent()
		this.productViewer.UpdateContent()
	}
}

//-----------------------------------------------------------------------------
// wx.Menu expansion
//-----------------------------------------------------------------------------
wx.Menu.AppendEx(id:number, text:string, img:image, kind:number => wx.ITEM_NORMAL) = {
	imgTmp = image(`rgba, img.width + 8, img.height)
	imgTmp.paste(0, 0, img)
	item = wx.MenuItem(this, id, text, kind => kind)
	item.SetBitmap(imgTmp)
	this.AppendItem(item)
}

//-----------------------------------------------------------------------------
// MainWindow
//-----------------------------------------------------------------------------
MainWindow = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.Point(g.xMainWindow, g.yMainWindow),
			size:wx.Size => wx.Size(g.wdMainWindow, g.htMainWindow)) = {|nil, wx.ID_ANY, title, pos, size|
		this.SetIcon(g.icon)
		menuBar = wx.MenuBar()
		wx.Menu {|m|
			menuBar.Append(m, '&File'$)
			m.AppendEx(ID_Open,			'&Open Image...\tCtrl-O'$,		g.imgOpen)
			m.AppendEx(ID_Close,		'&Close Image\tCtrl-L'$,		g.imgClose)
			m.AppendEx(ID_ShowList,		'&Show Image List...\tCtrl-S'$,	g.imgShowList)
			m.AppendEx(ID_Prev,			'&Previous\tCtrl-P'$,			g.imgPrev)
			m.AppendEx(ID_Next,			'&Next\tCtrl-N'$,				g.imgNext)
			m.AppendSeparator()
			m.Append(wx.ID_CLOSE,		'E&xit\tAlt-X'$)
		}
		wx.Menu {|m|
			menuBar.Append(m, '&Image'$)
			this.PrepareMenu_Image(m)
		}
		wx.Menu {|m|
			menuBar.Append(m, '&Output'$)
			this.PrepareMenu_Output(m)
		}
		wx.Menu {|m|
			menuBar.Append(m, '&Help'$)
			m.Append(wx.ID_ABOUT,		'&About...'$)
		}
		this.SetMenuBar(menuBar)
		this.CreateToolBar(wx.TB_FLAT) {|tb|
			tb.AddTool(ID_Open,			'Open Image'$,		g.imgOpen,			wx.NullBitmap, wx.ITEM_NORMAL,	'Open Image'$)
			tb.AddTool(ID_Close,		'Close Image'$,		g.imgClose,			wx.NullBitmap, wx.ITEM_NORMAL,	'Close Image'$)
			tb.AddTool(ID_ShowList,		'Show Image List'$,	g.imgShowList,		wx.NullBitmap, wx.ITEM_NORMAL,	'Show Image List'$)
			tb.AddSeparator()
			tb.AddTool(ID_Prev,			'Previous'$,		g.imgPrev,			wx.NullBitmap, wx.ITEM_NORMAL,	'Previous'$)
			tb.AddTool(ID_Next,			'Next'$,			g.imgNext,			wx.NullBitmap, wx.ITEM_NORMAL,	'Next'$)
			tb.AddSeparator()
			tb.AddTool(ID_RenameLabel,	'Rename Label'$,	g.imgRenameLabel,	wx.NullBitmap, wx.ITEM_NORMAL,	'Rename Label'$)
			tb.AddTool(ID_RotateL,		'Rotate Left'$,		g.imgRotateL,		wx.NullBitmap, wx.ITEM_NORMAL,	'Rotate Left'$)
			tb.AddTool(ID_RotateR,		'Rotate Right'$,	g.imgRotateR,		wx.NullBitmap, wx.ITEM_NORMAL,	'Rotate Right'$)
			tb.AddTool(ID_ColorAdjust,	'Color Adjustment'$,g.imgColorAdjust,	wx.NullBitmap, wx.ITEM_NORMAL,	'Color Adjustment'$)
			tb.AddSeparator()
			tb.AddTool(ID_TiltAdj,		'Tilt Adjust'$,		g.imgTiltAdj,		wx.NullBitmap, wx.ITEM_CHECK,	'Tilt Adjust'$)
			tb.AddTool(ID_FrostFrame,	'Frost Frame'$,		g.imgFrostFrame,	wx.NullBitmap, wx.ITEM_CHECK,	'Frost Frame'$)
			tb.AddTool(ID_CenterGuide,	'Center Guide'$,	g.imgCenterGuide,	wx.NullBitmap, wx.ITEM_CHECK,	'Center Guide'$)
			tb.AddSeparator()
			tb.AddTool(ID_OutputOption,	'Output Option'$,	g.imgOutputOption,	wx.NullBitmap, wx.ITEM_CHECK,	'Output Option'$)
			tb.AddStretchableSpace()
			wx.ComboBox(tb, wx.ID_ANY, size => wx.Size(200, -1), style => wx.CB_READONLY) {|ctrl|
				tb.AddControl(ctrl, 'Paper Size')
				ctrl.Append(format('%s (%d x %dmm)', g.paperSizeTbl:*name,
							g.paperSizeTbl:*shortSide, g.paperSizeTbl:*longSide)):void
				ctrl.Select(g.paperSizeTbl.find(g.paperSizeCur):index || 0)
				this.cbPaperSize = ctrl
				ctrl.Bind(wx.EVT_COMBOBOX) {|event| this.OnComboBox_PaperSize(event)}
			}
			tb.Realize()
		}
		wx.SashLayoutWindow(this, wx.ID_ANY,
					style => wx.BORDER_NONE | wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(1000, g.htSashTop));
			sash.SetOrientation(wx.LAYOUT_HORIZONTAL);
			sash.SetAlignment(wx.LAYOUT_TOP);
			sash.SetSashVisible(wx.SASH_BOTTOM, true);
			this.sashTop = sash
		}
		this.panelTop = wx.Panel(this.sashTop)
		wx.SashLayoutWindow(this.panelTop, wx.ID_ANY,
					style => wx.BORDER_NONE | wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(g.wdSashLeft, 1000));
			sash.SetOrientation(wx.LAYOUT_VERTICAL);
			sash.SetAlignment(wx.LAYOUT_LEFT);
			sash.SetSashVisible(wx.SASH_RIGHT, true);
			this.sashLeft = sash
		}
		this.panelLeft = wx.Panel(this.sashLeft)
		ProductViewer(this, wx.BORDER_SUNKEN) {|ctrl|
			ctrl.Bind(wx.EVT_RIGHT_DOWN) {|event| this.OnRightDown_ProductViewer(event)}
			this.productViewer = ctrl
		}
		Extractor(this.panelLeft, wx.BORDER_SUNKEN, this.productViewer) {|ctrl|
			ctrl.Bind(wx.EVT_RIGHT_DOWN) {|event| this.OnRightDown_Label(event)}
			this.extractor = ctrl
		}
		wx.BoxSizer(wx.VERTICAL) {|vbox|
			this.panelLeft.SetSizer(vbox)
			vbox.Add(this.extractor, wx.SizerFlags(1).Expand())
			wx.StaticText(this.panelLeft, ID_Label, '',
					style => wx.ALIGN_CENTRE_HORIZONTAL | wx.ST_NO_AUTORESIZE) {|ctrl|
				vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 2))
				ctrl.Bind(wx.EVT_RIGHT_DOWN) {|event| this.OnRightDown_Label(event)}
			}
		}
		wx.Notebook(this.panelTop, wx.ID_ANY, style => wx.NB_BOTTOM) {|ctrl|
			//ctrl.SetPadding(wx.Size(8, 0))
			this.noteBook = ctrl
		}
		wx.ListCtrl(this.noteBook, wx.ID_ANY, style => wx.BORDER_SUNKEN | wx.LC_REPORT) {|ctrl|
			this.noteBook.AddPage(ctrl, '  ' + 'Layout Frame List'$ + '  ')
			font = ctrl.GetFont()
			font.SetPointSize(font.GetPointSize() + 2)
			ctrl.SetFont(font)
			ctrl.InsertColumn(0.., ['Frame Size'$, 'Purpose'$])
			ctrl.Bind(wx.EVT_SIZE) {|event|
				size = event.GetSize()
				width = size.GetWidth() - 25
				wdColumn = 150
				ctrl.SetColumnWidth(0, wdColumn)
				width -= wdColumn
				ctrl.SetColumnWidth(1, width)
				event.Skip()
			}
			nLayoutFramesUserDef = 0
			g.layoutFrameTbl.each {|layoutFrame, idx|
				wx.ListItem {|item|
					item.SetId(idx)
					if (layoutFrame.userDefFlag) {
						nLayoutFramesUserDef += 1
						item.SetText('User defined'$ + format(' %d', nLayoutFramesUserDef))
					} else {
						item.SetText('%d x %dmm' % [layoutFrame.htFrame, layoutFrame.wdFrame])
					}
					ctrl.InsertItem(item)
				}
				wx.ListItem {|item|
					item.SetId(idx)
					item.SetColumn(1)
					if (layoutFrame.userDefFlag && layoutFrame.purpose.isempty()) {
						item.SetText('(double-click to edit)'$)
					} else {
						item.SetText(layoutFrame.purpose)
					}
					ctrl.SetItem(item)
				}
			}
			ctrl.SetItemState(0,
					wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED,
					wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED)
			ctrl.Bind(wx.EVT_LIST_ITEM_SELECTED) {|event| this.OnListItemSelected(event) }
			ctrl.Bind(wx.EVT_LIST_ITEM_ACTIVATED) {|event| this.OnListItemActivated(event) }
			this.listCtrlLayoutFrame = ctrl
		}
		this.dlgOutputOption = OutputOptionDialog(this, this.productViewer)
		this.listCtrlLayoutFrame.SetFocus()
		this.Bind(wx.EVT_MOVE) {|event| this.UpdateSizePosInfo(), event.Skip()}
		this.Bind(wx.EVT_MENU, wx.ID_CLOSE)			{|event| this.Close()}
		this.Bind(wx.EVT_MENU, ID_Open)				{|event| this.OnMenu_Open(event)}
		this.Bind(wx.EVT_MENU, ID_Close)			{|event| this.OnMenu_Close(event)}
		this.Bind(wx.EVT_MENU, ID_ShowList)			{|event| this.OnMenu_ShowList(event)}
		this.Bind(wx.EVT_MENU, ID_Prev)				{|event| this.OnMenu_Prev(event)}
		this.Bind(wx.EVT_MENU, ID_Next)				{|event| this.OnMenu_Next(event)}
		this.Bind(wx.EVT_MENU, ID_OutputPDF)		{|event| this.OnMenu_OutputPDF(event)}
		this.Bind(wx.EVT_MENU, ID_LaunchPDFViewer)	{|event| this.OnMenu_LaunchPDFViewer(event)}
		this.Bind(wx.EVT_MENU, ID_PrintService)		{|event| this.OnMenu_PrintService(event)}
		this.Bind(wx.EVT_MENU, ID_RenameLabel)		{|event| this.OnMenu_RenameLabel(event)}
		this.Bind(wx.EVT_MENU, ID_RotateL)			{|event| this.OnMenu_RotateL(event)}
		this.Bind(wx.EVT_MENU, ID_RotateR)			{|event| this.OnMenu_RotateR(event)}
		this.Bind(wx.EVT_MENU, ID_ColorAdjust)		{|event| this.OnMenu_ColorAdjust(event)}
		this.Bind(wx.EVT_MENU, ID_TiltAdj)			{|event| this.OnMenu_TiltAdj(event)}
		this.Bind(wx.EVT_MENU, ID_FrostFrame)		{|event| this.OnMenu_FrostFrame(event)}
		this.Bind(wx.EVT_MENU, ID_CenterGuide)		{|event| this.OnMenu_CenterGuide(event)}
		this.Bind(wx.EVT_MENU, ID_OutputOption)		{|event| this.OnMenu_OutputOption(event)}
		this.Bind(wx.EVT_MENU, ID_CopyExtractedImage) {|event| this.OnMenu_CopyExtractedImage(event)}
		this.Bind(wx.EVT_MENU, ID_SaveExtractedImage) {|event| this.OnMenu_SaveExtractedImage(event)}
		this.Bind(wx.EVT_MENU, wx.ID_ABOUT)			{|event| this.OnMenu_About(event)}
		this.Bind(wx.EVT_SIZE) {|event|
			wx.LayoutAlgorithm().LayoutWindow(this, this.productViewer)
			this.UpdateSizePosInfo()
			event.Skip()
		}
		this.Bind(wx.EVT_SASH_DRAGGED) {|event|
			if (event.GetDragStatus() != wx.SASH_STATUS_OUT_OF_RANGE) {
				id = event.GetId()
				if (id == this.sashTop.GetId()) {
					g.htSashTop = event.GetDragRect().GetHeight() + 2
					this.sashTop.SetDefaultSize(wx.Size(1000, g.htSashTop))
				}
				wx.LayoutAlgorithm().LayoutWindow(this, this.productViewer)
				this.productViewer.Refresh()
			}
		}
		this.panelTop.Bind(wx.EVT_SIZE) {|event|
			wx.LayoutAlgorithm().LayoutWindow(this.panelTop, this.noteBook)
			event.Skip()
		}
		this.panelTop.Bind(wx.EVT_SASH_DRAGGED) {|event|
			if (event.GetDragStatus() != wx.SASH_STATUS_OUT_OF_RANGE) {
				id = event.GetId()
				if (id == this.sashLeft.GetId()) {
					g.wdSashLeft = event.GetDragRect().GetWidth() + 2
					this.sashLeft.SetDefaultSize(wx.Size(g.wdSashLeft, 1000))
				}
				wx.LayoutAlgorithm().LayoutWindow(this.panelTop, this.noteBook)
				this.noteBook.Refresh()
			}
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_Label) {|event|
			text = if (g.imgInfoCur.fileName.isempty()) {
				'(drag image into above area)'$
			} else {
				g.imgInfoCur.label
			}
			event.SetText(text)
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_Close) {|event|
			event.Enable(!g.imgInfoCur.fileName.isempty())
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_RenameLabel) {|event|
			event.Enable(!g.imgInfoCur.fileName.isempty())
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_ShowList) {|event|
			event.Enable(!g.imgInfos.isempty())
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_Prev) {|event|
			idx = g.imgInfos.find(g.imgInfoCur):index
			event.Enable(idx && idx > 0)
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_Next) {|event|
			idx = g.imgInfos.find(g.imgInfoCur):index
			event.Enable(idx && idx < g.imgInfos.len() - 1)
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_TiltAdj) {|event|
			event.Check(g.imgInfoCur.tiltAdjFlag)
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_FrostFrame) {|event|
			event.Check(g.frostFrameFlag)
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_CenterGuide) {|event|
			event.Check(g.centerGuideFlag)
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_OutputOption) {|event|
			event.Check(this.dlgOutputOption.IsShown())
		}
		this.Bind(wx.EVT_UPDATE_UI,	ID_PrintService) {|event|
			event.Enable(g.paperSizeCur.printServiceFlag)
		}
	}
	UpdateSizePosInfo() = {
		(this.IsIconized() || this.IsMaximized()) && return
		[g.xMainWindow, g.yMainWindow] = this.GetScreenPositionXY()
		[g.wdMainWindow, g.htMainWindow] = this.GetSizeWH()
	}
	UpdateWholeContent(blankFlag:boolean => false) = {
		this.extractor.UpdateContent(blankFlag)
		this.productViewer.UpdateContent(blankFlag)
	}
	ProcessImage() {block} = {
		wx.BusyCursor {
			this.UpdateWholeContent(true)
			block()
			this.UpdateWholeContent()
		}
	}
	PrepareMenu_Image(m:wx.Menu) = {
		m.AppendEx(ID_RenameLabel,			'&Rename Label...'$,		g.imgRenameLabel)
		m.AppendEx(ID_RotateL,				'Rotate &Left\tCtrl-L'$,	g.imgRotateL)
		m.AppendEx(ID_RotateR,				'Rotate R&ight\tCtrl-I'$,	g.imgRotateR)
		m.AppendEx(ID_ColorAdjust,			'Color &Adjustment...'$,	g.imgColorAdjust)
		m.AppendSeparator()
		m.AppendCheckItem(ID_TiltAdj,		'&Tilt Adjust&t\tCtrl-T'$)
		m.AppendCheckItem(ID_FrostFrame,	'&Frost Frame'$)
		m.AppendCheckItem(ID_CenterGuide,	'C&enter Guide'$)
		m.AppendSeparator()
		m.Append(ID_CopyExtractedImage,		'&Copy Extracted Image\tCtrl-C'$)
		m.Append(ID_SaveExtractedImage,		'&Save Extracted Image...'$)
	}
	PrepareMenu_Output(m:wx.Menu) = {
		m.AppendCheckItem(ID_OutputOption,	'&Output Option...'$)
		m.AppendSeparator()
		m.AppendEx(ID_OutputPDF,			'O&utput PDF...'$,			g.imgOutputPDF)
		m.AppendEx(ID_LaunchPDFViewer,		'Launch PDF &Viewer...'$,	g.imgLaunchPDFViewer)
		m.AppendSeparator()
		m.AppendEx(ID_PrintService,			'&Print Service...'$,		g.imgPrintService)
	}
	OnListItemSelected(event:wx.ListEvent) = {
		idx = event.GetIndex()
		(idx < 0) && return
		g.layoutFrameCur = g.layoutFrameTbl[idx]
		this.Update()
		wx.BusyCursor {
			this.UpdateWholeContent()
		}
	}
	OnListItemActivated(event:wx.ListEvent) = {
		idx = event.GetIndex()
		(idx < 0) && return
		layoutFrame = g.layoutFrameTbl[idx]
		!layoutFrame.userDefFlag && return
		dlg = LayoutFrameEditorDialog(this, this.extractor, this.productViewer, layoutFrame)
		if (dlg.ShowModal() == wx.ID_OK) {
			wx.ListItem {|item|
				item.SetId(idx)
				item.SetColumn(1)
				if (layoutFrame.purpose.isempty()) {
					item.SetText('(double-click to edit)'$)
				} else {
					item.SetText(layoutFrame.purpose)
				}
				this.listCtrlLayoutFrame.SetItem(item)
			}
		}
	}
	OnRightDown_Label(event:wx.MouseEvent) = {
		wx.Menu() {|m|
			this.PrepareMenu_Image(m)
			this.PopupMenu(m)
		}
	}
	OnRightDown_ProductViewer(event:wx.MouseEvent) = {
		wx.Menu() {|m|
			this.PrepareMenu_Output(m)
			this.PopupMenu(m)
		}
	}
	OnComboBox_PaperSize(event:wx.CommandEvent) = {
		g.paperSizeCur = g.paperSizeTbl[this.cbPaperSize.GetCurrentSelection()]
		wx.BusyCursor {
			this.productViewer.UpdateContent(this)
			this.productViewer.UpdateContent()
		}
	}
	OnMenu_Open(event:wx.CommandEvent) = {
		filter = 'Image files (*.jpg;*.png;*.gif;*.bmp)|*.jpg;*.png;*.gif;*.bmp|All files (*.*)|*.*'$
		dlg = wx.FileDialog(this, 'Choose an image file'$,
								'', g.imgInfoCur.fileName, filter, wx.FD_OPEN)
		if (dlg.ShowModal() == wx.ID_OK) {
			fileName = dlg.GetPath()
			ReadImageFile(this, fileName)
			this.UpdateWholeContent()
		}
	}
	OnMenu_Close(event:wx.CommandEvent) = {
		!(idx = g.imgInfos.find(g.imgInfoCur):index) && return
		wx.MessageDialog(this, 'Are you sure to close the current image?'$,
				'Confirm'$, wx.OK | wx.CANCEL | wx.OK_DEFAULT | \
										wx.ICON_QUESTION | wx.CENTRE) {|dlg|
			(dlg.ShowModal() != wx.ID_OK) && return
		}
		g.imgInfos.erase(idx)
		if (g.imgInfos.isempty()) {
			this.ProcessImage {
				SetBlankImage()
			}
		} else {
			if (idx >= g.imgInfos.len()) {
				idx = g.imgInfos.len() - 1
			}
			imgInfo = g.imgInfos[idx]
			this.ProcessImage {
				ReadImageFile(this, imgInfo.fileName)
			}
		}
	}
	OnMenu_ShowList(event:wx.CommandEvent) = {
		g.imgInfos.isempty() && return
		dlg = ImageListDialog(this, g.imgInfos, g.imgInfos.find(g.imgInfoCur):index)
		dlg.ShowModal()
		g.imgInfos = dlg.GetImageInfos()
		if (imgInfo = dlg.GetImageInfo()) {
			this.ProcessImage {
				ReadImageFile(this, imgInfo.fileName)
			}
		}
	}
	OnMenu_Prev(event:wx.CommandEvent) = {
		idx = g.imgInfos.find(g.imgInfoCur):index
		if (idx && idx > 0) {
			imgInfo = g.imgInfos[idx - 1]
			this.ProcessImage {
				ReadImageFile(this, imgInfo.fileName)
			}
		}
	}
	OnMenu_Next(event:wx.CommandEvent) = {
		idx = g.imgInfos.find(g.imgInfoCur):index
		if (idx && idx < g.imgInfos.len() - 1) {
			imgInfo = g.imgInfos[idx + 1]
			this.ProcessImage {
				ReadImageFile(this, imgInfo.fileName)
			}
		}
	}
	OnMenu_OutputOption(event:wx.CommandEvent) = {
		if (event.IsChecked()) {
			this.dlgOutputOption.InitWindowValues()
			this.dlgOutputOption.Show(true)
		} else {
			this.dlgOutputOption.Show(false)
		}
	}
	OnMenu_OutputPDF(event:wx.CommandEvent) = {
		filter = 'PDF file (*.pdf)|*.pdf|All files (*.*)|*.*'$
		dlg = wx.FileDialog(this, 'Specify a PDF file to output'$,
							'', '', filter, wx.FD_SAVE | wx.FD_OVERWRITE_PROMPT)
		if (dlg.ShowModal() == wx.ID_OK) {
			fileNamePDF = dlg.GetPath()
			orientation = g.paperSizeCur.GetBetterOrientation(g.layoutFrameCur, g.orientation)
			ComposeProductPDF(fileNamePDF, g.imgPhotoHighRes,
								g.layoutFrameCur, g.paperSizeCur, orientation)
		}
	}
	OnMenu_LaunchPDFViewer(event:wx.CommandEvent) = {
		orientation = g.paperSizeCur.GetBetterOrientation(g.layoutFrameCur, g.orientation)
		ComposeProductPDF(g.fileNameTmpPDF, g.imgPhotoHighRes,
							g.layoutFrameCur, g.paperSizeCur, orientation)
		try {
			OpenWithApp(g.fileNameTmpPDF)
		} catch {
			wx.MessageDialog(parent, 'Failed to launch PDF viewer'$,
							'Error'$, wx.OK | wx.ICON_ERROR | wx.CENTRE) {|dlg|
				dlg.ShowModal()
			}
		}
	}
	OnMenu_PrintService(event:wx.CommandEvent) = {
		nAdjLinesDrawn = 15
		dpi = 600
		dlg = PrintServiceDialog(this, nAdjLinesDrawn, g.printServiceName,
					g.nAdjLines_a, g.nAdjLines_b, g.nAdjLines_c, g.nAdjLines_d)
		dlg.CentreOnParent()
		(dlg.ShowModal() != wx.ID_OK) && return
		g.printServiceName	= dlg.printServiceName
		g.nAdjLines_a		= dlg.nAdjLines_a
		g.nAdjLines_b		= dlg.nAdjLines_b
		g.nAdjLines_c		= dlg.nAdjLines_c
		g.nAdjLines_d		= dlg.nAdjLines_d
		filter = 'JPEG file (*.jpg)|*.jpg|PNG file (*.png)|*.png|BMP file (*.bmp)|*.bmp|GIF file (*.gif)|*.gif'$
		dlg = wx.FileDialog(this, 'Specify an image file to save'$,
							'', '', filter, wx.FD_SAVE | wx.FD_OVERWRITE_PROMPT)
		(dlg.ShowModal() != wx.ID_OK) && return
		fileName = dlg.GetPath()
		orientation = g.paperSizeCur.GetBetterOrientation(g.layoutFrameCur, g.orientation)
		wx.BusyCursor {
			ComposeProductForPrintService(fileName, g.imgPhotoHighRes, g.layoutFrameCur,
					g.paperSizeCur, orientation, dpi,
					nAdjLinesDrawn, g.printServiceName,
					g.nAdjLines_a, g.nAdjLines_b, g.nAdjLines_c, g.nAdjLines_d)
		}
	}
	OnMenu_RenameLabel(event:wx.CommandEvent) = {
		dlg = RenameLabelDialog(this, g.imgInfoCur)
		dlg.CentreOnParent()
		if (dlg.ShowModal() == wx.ID_OK) {
			wx.BusyCursor {
				this.productViewer.UpdateContent(this)
				this.productViewer.UpdateContent()
			}
		}
	}
	OnMenu_RotateL(event:wx.CommandEvent) = {
		this.ProcessImage {
			RotateImageL()
		}
	}
	OnMenu_RotateR(event:wx.CommandEvent) = {
		this.ProcessImage {
			RotateImageR()
		}
	}
	OnMenu_ColorAdjust(event:wx.CommandEvent) = {
		dlg = ColorAdjustDialog(this, this.extractor, this.productViewer)
		dlg.ShowModal()
	}
	OnMenu_TiltAdj(event:wx.CommandEvent) = {
		g.imgInfoCur.tiltAdjFlag = event.IsChecked()
		if (g.imgInfoCur.tiltAdjFlag) {
			this.extractor.Refresh()
		} else {
			xrateMid = (g.imgInfoCur.xrateTop + g.imgInfoCur.xrateBtm) / 2
			g.imgInfoCur.xrateTop = g.imgInfoCur.xrateBtm = xrateMid
			this.extractor.Refresh()
			wx.BusyCursor {
				this.productViewer.UpdateContent(this)
				this.productViewer.UpdateContent()
			}
		}
	}
	OnMenu_FrostFrame(event:wx.CommandEvent) = {
		g.frostFrameFlag = event.IsChecked()
		this.extractor.Refresh()
	}
	OnMenu_CenterGuide(event:wx.CommandEvent) = {
		g.centerGuideFlag = event.IsChecked()
		this.extractor.Refresh()
	}
	OnMenu_CopyExtractedImage(event:wx.CommandEvent) = {
		imgExtract = ExtractImage(g.imgPhotoHighRes, g.layoutFrameCur, 0, 0)
		if (wx.TheClipboard.Open()) {
			wx.TheClipboard.SetData(wx.BitmapDataObject(imgExtract))
			wx.TheClipboard.Close()
		}
	}
	OnMenu_SaveExtractedImage(event:wx.CommandEvent) = {
		filter = 'JPEG file (*.jpg)|*.jpg|PNG file (*.png)|*.png|BMP file (*.bmp)|*.bmp|GIF file (*.gif)|*.gif'$
		dlg = wx.FileDialog(this, 'Specify an image file to save'$,
							'', '', filter, wx.FD_SAVE | wx.FD_OVERWRITE_PROMPT)
		if (dlg.ShowModal() == wx.ID_OK) {
			fileName = dlg.GetPath()
			imgExtract = ExtractImage(g.imgPhotoHighRes, g.layoutFrameCur, 0, 0)
			try {
				wx.BusyCursor {
					imgExtract.write(fileName)
				}
			} catch {
				wx.MessageDialog(this, 'Failed to save an image file'$,
								'Error'$, wx.OK | wx.ICON_ERROR | wx.CENTRE) {|dlg|
					dlg.ShowModal()
				}
			}
		}
	}
	OnMenu_About(event:wx.CommandEvent) = {
		dlg = AboutDialog(this)
		dlg.CentreOnParent()
		dlg.ShowModal()
	}
}

//-----------------------------------------------------------------------------
// ImageListDialog
//-----------------------------------------------------------------------------
ImageListDialog = class(wx.Dialog) {
	__init__(parent:wx.Window, imgInfos[]:ImageInfo, idxSel:number) = {|parent, wx.ID_ANY,
				'Image List'$, wx.Point(g.xImageListDialog, g.yImageListDialog),
				wx.Size(640, 480), style => wx.DEFAULT_DIALOG_STYLE|
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		this.imgInfos = imgInfos
		this.imgInfoCur = nil
		wx.ListView(this, wx.ID_ANY, style => wx.LC_ICON | wx.LC_SINGLE_SEL | \
								wx.LC_AUTOARRANGE | wx.LC_EDIT_LABELS) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand())
			this.listCtrl = ctrl
			ctrl.Bind(wx.EVT_LIST_ITEM_ACTIVATED) {|event| this.OnListItemActivated(event)}
			ctrl.Bind(wx.EVT_LIST_ITEM_RIGHT_CLICK) {|event| this.OnListItemRightClick(event)}
			ctrl.Bind(wx.EVT_LIST_KEY_DOWN) {|event| this.OnListKeyDown(event)}
			ctrl.Bind(wx.EVT_LIST_END_LABEL_EDIT) {|event| this.OnListEndLabelEdit(event)}
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Border(wx.TOP, 4).Expand())
			hbox.AddSpacer(16)
			hbox.AddStretchSpacer(1)
			wx.Button(this, wx.ID_OK, 'Select'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
				ctrl.SetDefault()
			}
			wx.Button(this, wx.ID_CANCEL, 'Close'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16))
			}
			hbox.AddSpacer(16)
		}
		wx.BusyCursor {
			this.PrepareContent(idxSel)
		}
		this.idxImgInfoProtected = idxSel
		this.Bind(wx.EVT_MOVE) {|event| this.UpdateSizePosInfo(), event.Skip()}
		this.Bind(wx.EVT_BUTTON, wx.ID_OK) {|event| this.OnButton_OK(event)}
		this.Bind(wx.EVT_MENU, ID_RenameLabel) {|event| this.OnMenu_RenameLabel(event)}
		this.Bind(wx.EVT_MENU, ID_Remove) {|event| this.OnMenu_Remove(event)}
		this.Bind(wx.EVT_UPDATE_UI, wx.ID_OK) {|event|
			idx = this.listCtrl.GetFirstSelected()
			event.Enable(idx >= 0)
		}
		this.Bind(wx.EVT_UPDATE_UI, ID_Remove) {|event|
			idx = this.listCtrl.GetFirstSelected()
			if (idx < 0) {
				event.Enable(false)
			} else {
				idxImgInfo = this.listCtrl.GetItemData(idx)
				event.Enable(idxImgInfo != this.idxImgInfoProtected)
			}
		}
	}
	UpdateSizePosInfo() = {
		(this.IsIconized() || this.IsMaximized()) && return
		[g.xImageListDialog, g.yImageListDialog] = this.GetScreenPositionXY()
	}
	PrepareContent(idxSel:number) = {
		size = 128
		imgList = wx.ImageList(size, size)
		this.imgInfos.each {|imgInfo, idx|
			if (!imgInfo.imgPhotoRaw) {
				imgInfo.imgPhotoRaw = image(imgInfo.fileName)
			}
			imgPhotoRaw = imgInfo.imgPhotoRaw
			imgSrc = (imgPhotoRaw.thumbnail(size):box).rotate(imgInfo.rotate)
			img = image(`rgb, size, size, `gray)
			img.paste((size - imgSrc.width) / 2, (size - imgSrc.height) / 2, imgSrc)
			bmp:wx.Bitmap = img
			if (idx == idxSel) {
				wx.MemoryDC(bmp) {|dc|
					dc.DrawBitmap(g.imgTick, 2, 2, false)
				}
			}
			imgList.Add(bmp)
		}
		this.listCtrl.SetImageList(imgList, wx.IMAGE_LIST_NORMAL)
		this.imgInfos.each {|imgInfo, idx|
			this.listCtrl.InsertImageStringItem(idx, imgInfo.label, idx)
			this.listCtrl.SetItemData(idx, idx)
		}
		this.listCtrl.SetItemTextColour(idxSel, wx.Colour(128, 0, 0))
		this.listCtrl.SetItemState(idxSel,
				wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED,
				wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED)
		this.listCtrl.EnsureVisible(idxSel)
	}
	GetImageInfos() = this.imgInfos
	GetImageInfo() = this.imgInfoCur
	UpdateImageInfos() = {
		this.imgInfos = repeat (this.listCtrl.GetItemCount()):list {|idx|
			idxImgInfo = this.listCtrl.GetItemData(idx)
			this.imgInfos[idxImgInfo]
		}
	}
	RemoveSelectedImage() = {
		idx = this.listCtrl.GetFirstSelected()
		if (idx < 0) {
			wx.Bell()
			return
		}
		idxImgInfo = this.listCtrl.GetItemData(idx)
		if (idxImgInfo == this.idxImgInfoProtected) {
			wx.Bell()
			return
		}
		wx.MessageDialog(this,
				R'''
				Are you sure to remove the image from the list?
				
				It will not delete the original file.'''$,
				'Confirm'$, wx.OK | wx.CANCEL | wx.OK_DEFAULT | \
						wx.ICON_QUESTION | wx.CENTRE) {|dlg|
			(dlg.ShowModal() != wx.ID_OK) && return
		}
		this.listCtrl.DeleteItem(idx)
		this.listCtrl.Update()
		if (idx >= this.listCtrl.GetItemCount()) {
			idx = this.listCtrl.GetItemCount() - 1
		}
		this.listCtrl.SetItemState(idx,
				wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED,
				wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED)
	}
	OnButton_OK(event:wx.CommandEvent) = {
		((idx = this.listCtrl.GetFirstSelected()) < 0) && return
		idxImgInfo = this.listCtrl.GetItemData(idx)
		this.imgInfoCur = this.imgInfos[idxImgInfo]
		this.UpdateImageInfos()
		this.EndModal(wx.ID_OK)
	}
	OnMenu_RenameLabel(event:wx.CommandEvent) = {
		((idx = this.listCtrl.GetFirstSelected()) < 0) && return
		this.listCtrl.EditLabel(idx)
	}
	OnMenu_Remove(event:wx.CommandEvent) = {
		this.RemoveSelectedImage()
	}
	OnListItemActivated(event:wx.ListEvent) = {
		((idx = event.GetIndex()) < 0) && return
		idxImgInfo = this.listCtrl.GetItemData(idx)
		this.imgInfoCur = this.imgInfos[idxImgInfo]
		this.UpdateImageInfos()
		this.EndModal(wx.ID_OK)
	}
	OnListItemRightClick(event:wx.ListEvent) = {
		wx.Menu() {|m|
			m.AppendEx(ID_RenameLabel,	'&Rename Label'$,	g.imgRenameLabel)
			m.AppendEx(ID_Remove,		'R&emove'$,			g.imgClose)
			this.PopupMenu(m)
		}
	}
	OnListKeyDown(event:wx.ListEvent) = {
		if (event.GetKeyCode() == wx.WXK_DELETE) {
			this.RemoveSelectedImage()
		}
	}
	OnListEndLabelEdit(event:wx.ListEvent) = {
		((idx = event.GetIndex()) < 0) && return
		idxImgInfo = this.listCtrl.GetItemData(idx)
		this.imgInfos[idxImgInfo].label = event.GetText()
	}
}

//-----------------------------------------------------------------------------
// ColorAdjustDialog
//-----------------------------------------------------------------------------
ColorAdjustDialog = class(wx.Dialog) {
	wdLabel = 80
	wdSlider = 260
	wdText = 60
	wdButton = 80
	__init__(parent:wx.Window, extractor:Extractor, productViewer:ProductViewer) = {|parent, wx.ID_ANY,
				'Color Adjustment'$,
				pos => wx.Point(g.xColorAdjustDialog, g.yColorAdjustDialog)|
		this.extractor = extractor
		this.productViewer = productViewer
		this.imgInfo = g.imgInfoCur
		this.colorTone_init		= this.imgInfo.colorTone
		this.brightness_init	= this.imgInfo.brightness
		this.contrast_init		= this.imgInfo.contrast
		this.redness_init		= this.imgInfo.redness
		this.greenness_init		= this.imgInfo.greenness
		this.blueness_init		= this.imgInfo.blueness
		this.sepiaR_init		= g.sepiaR
		this.sepiaG_init		= g.sepiaG
		this.sepiaB_init		= g.sepiaB
		
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.TOP | wx.BOTTOM, 8))
		vbox.AddSpacer(8)
		wx.BoxSizer(wx.VERTICAL) {|vboxIn|
			vbox.Add(vboxIn, wx.SizerFlags().Expand().Border(wx.LEFT | wx.RIGHT, 8))
			vbox:local = vboxIn
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgBrightness)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(this, wx.ID_ANY, 'Brightness'$, size => wx.Size(wdLabel, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 4))
				}
				wx.Slider(this, wx.ID_ANY, this.imgInfo.brightness,
									-100, 100, size => wx.Size(wdSlider, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sldrBrightness = ctrl
					ctrl.Bind(wx.EVT_SCROLL) {|event|
						pos = event.GetPosition()
						this.imgInfo.brightness = pos
						this.textBrightness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
				wx.TextCtrl(this, wx.ID_ANY, this.imgInfo.brightness,
						size => wx.Size(wdText, -1), style => wx.TE_CENTRE | wx.TE_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.textBrightness = ctrl
				}
				wx.Button(this, wx.ID_ANY, 'Reset'$, size => wx.Size(wdButton, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
					ctrl.Bind(wx.EVT_BUTTON) {
						pos = this.brightness_init
						this.imgInfo.brightness = pos
						this.sldrBrightness.SetValue(pos)
						this.textBrightness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
			}
			vbox.AddSpacer(8)
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgContrast)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(this, wx.ID_ANY, 'Contrast'$, size => wx.Size(wdLabel, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 4))
				}
				wx.Slider(this, wx.ID_ANY, this.imgInfo.contrast,
									-100, 100, size => wx.Size(wdSlider, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sldrContrast = ctrl
					ctrl.Bind(wx.EVT_SCROLL) {|event|
						pos = event.GetPosition()
						this.imgInfo.contrast = pos
						this.textContrast.SetValue(pos)
						this.UpdateExtractor()
					}
				}
				wx.TextCtrl(this, wx.ID_ANY, this.imgInfo.contrast,
						size => wx.Size(wdText, -1), style => wx.TE_CENTRE | wx.TE_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.textContrast = ctrl
				}
				wx.Button(this, wx.ID_ANY, 'Reset'$, size => wx.Size(wdButton, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
					ctrl.Bind(wx.EVT_BUTTON) {
						pos = this.contrast_init
						this.imgInfo.contrast = pos
						this.sldrContrast.SetValue(pos)
						this.textContrast.SetValue(pos)
						this.UpdateExtractor()
					}
				}
			}
		}
		vbox.AddSpacer(16)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		vbox.AddSpacer(16)
		wx.BoxSizer(wx.VERTICAL) {|vboxIn|
			vbox.Add(vboxIn, wx.SizerFlags().Expand().Border(wx.LEFT | wx.RIGHT, 8))
			vbox:local = vboxIn
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgBoxRed)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(this, wx.ID_ANY, 'Redness'$, size => wx.Size(wdLabel, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 4))
				}
				wx.Slider(this, wx.ID_ANY, this.imgInfo.redness,
									-100, 100, size => wx.Size(wdSlider, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sldrRedness = ctrl
					ctrl.Bind(wx.EVT_SCROLL) {|event|
						pos = event.GetPosition()
						this.imgInfo.redness = pos
						this.textRedness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
				wx.TextCtrl(this, wx.ID_ANY, this.imgInfo.redness,
						size => wx.Size(wdText, -1), style => wx.TE_CENTRE | wx.TE_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.textRedness = ctrl
				}
				wx.Button(this, wx.ID_ANY, 'Reset'$, size => wx.Size(wdButton, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
					ctrl.Bind(wx.EVT_BUTTON) {
						pos = this.redness_init
						this.imgInfo.redness = pos
						this.sldrRedness.SetValue(pos)
						this.textRedness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
			}
			vbox.AddSpacer(8)
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgBoxGreen)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(this, wx.ID_ANY, 'Greenness'$, size => wx.Size(wdLabel, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 4))
				}
				wx.Slider(this, wx.ID_ANY, this.imgInfo.greenness,
									-100, 100, size => wx.Size(wdSlider, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sldrGreenness = ctrl
					ctrl.Bind(wx.EVT_SCROLL) {|event|
						pos = event.GetPosition()
						this.imgInfo.greenness = pos
						this.textGreenness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
				wx.TextCtrl(this, wx.ID_ANY, this.imgInfo.greenness,
						size => wx.Size(wdText, -1), style => wx.TE_CENTRE | wx.TE_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.textGreenness = ctrl
				}
				wx.Button(this, wx.ID_ANY, 'Reset'$, size => wx.Size(wdButton, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
					ctrl.Bind(wx.EVT_BUTTON) {
						pos = this.greenness_init
						this.imgInfo.greenness = pos
						this.sldrGreenness.SetValue(pos)
						this.textGreenness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
			}
			vbox.AddSpacer(8)
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgBoxBlue)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(this, wx.ID_ANY, 'Blueness'$, size => wx.Size(wdLabel, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 4))
				}
				wx.Slider(this, wx.ID_ANY, this.imgInfo.blueness,
									-100, 100, size => wx.Size(wdSlider, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sldrBlueness = ctrl
					ctrl.Bind(wx.EVT_SCROLL) {|event|
						pos = event.GetPosition()
						this.imgInfo.blueness = pos
						this.textBlueness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
				wx.TextCtrl(this, wx.ID_ANY, this.imgInfo.blueness,
						size => wx.Size(wdText, -1), style => wx.TE_CENTRE | wx.TE_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.textBlueness = ctrl
				}
				wx.Button(this, wx.ID_ANY, 'Reset'$, size => wx.Size(wdButton, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
					ctrl.Bind(wx.EVT_BUTTON) {
						pos = this.blueness_init
						this.imgInfo.blueness = pos
						this.sldrBlueness.SetValue(pos)
						this.textBlueness.SetValue(pos)
						this.UpdateExtractor()
					}
				}
			}
		}
		vbox.AddSpacer(16)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		vbox.AddSpacer(16)
		wx.BoxSizer(wx.VERTICAL) {|vboxIn|
			vbox.Add(vboxIn, wx.SizerFlags().Expand().Border(wx.LEFT | wx.RIGHT, 8))
			vbox:local = vboxIn
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgColorTone)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(this, wx.ID_ANY, 'Color Tone'$, size => wx.Size(wdLabel, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 4))
				}
				wx.RadioButton(this, wx.ID_ANY, 'Color'$, style => wx.RB_GROUP) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					ctrl.SetValue(this.imgInfo.colorTone == `color)
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						this.imgInfo.colorTone = `color
						this.UpdateExtractor()
					}
				}
				wx.RadioButton(this, wx.ID_ANY, 'Monochrome'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 20).Align(wx.ALIGN_CENTRE_VERTICAL))
					ctrl.SetValue(this.imgInfo.colorTone == `mono)
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						this.imgInfo.colorTone = `mono
						this.UpdateExtractor()
					}
				}
				wx.RadioButton(this, wx.ID_ANY, 'Sepia'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 20).Align(wx.ALIGN_CENTRE_VERTICAL))
					ctrl.SetValue(this.imgInfo.colorTone == `sepia)
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						this.imgInfo.colorTone = `sepia
						this.UpdateExtractor()
					}
				}
				wx.Button(this, ID_Adjust, 'Adjust'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 12).Align(wx.ALIGN_CENTRE_VERTICAL))
					
				}
			}
		}
		vbox.AddSpacer(16)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Centre().Border(wx.TOP, 8))
			wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
				ctrl.SetDefault()
				ctrl.SetFocus()
			}
			wx.Button(this, wx.ID_CANCEL, 'Cancel'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16))
			}
		}
		this.Fit()
		this.Bind(wx.EVT_MOVE) {|event| this.UpdateSizePosInfo(), event.Skip()}
		this.Bind(wx.EVT_BUTTON, ID_Adjust) {|event| this.OnButton_Adjust(event)}
		this.Bind(wx.EVT_BUTTON, wx.ID_OK) {|event| this.OnButton_OK(event)}
		this.Bind(wx.EVT_BUTTON, wx.ID_CANCEL) {|event| this.OnButton_CANCEL(event)}
		this.Bind(wx.EVT_UPDATE_UI, ID_Adjust) {|event|
			event.Enable(this.imgInfo.colorTone == `sepia)
		}
	}
	OnButton_Adjust(event:wx.CommandEvent) = {
		data = wx.ColourData()
		data.SetColour(wx.Colour(g.sepiaR, g.sepiaG, g.sepiaB))
		dlg = wx.ColourDialog(this, data)
		if (dlg.ShowModal() == wx.ID_OK) {
			c = dlg.GetColourData().GetColour()
			[g.sepiaR, g.sepiaG, g.sepiaB] = [c.Red(), c.Green(), c.Blue()]
			this.UpdateExtractor()
		}
	}
	OnButton_OK(event:wx.CommandEvent) = {
		wx.BusyCursor {
			this.productViewer.UpdateContent(this)
			this.productViewer.UpdateContent()
		}
		this.EndModal(wx.ID_OK)
	}
	OnButton_CANCEL(event:wx.CommandEvent) = {
		this.CancelModification()
		this.EndModal(wx.ID_CANCEL)
	}
	CancelModification() = {
		this.imgInfo.colorTone	= this.colorTone_init
		this.imgInfo.brightness	= this.brightness_init
		this.imgInfo.contrast	= this.contrast_init
		this.imgInfo.redness	= this.redness_init
		this.imgInfo.greenness	= this.greenness_init
		this.imgInfo.blueness	= this.blueness_init
		g.sepiaR				= this.sepiaR_init
		g.sepiaG				= this.sepiaG_init
		g.sepiaB				= this.sepiaB_init
		this.UpdateExtractor()
	}
	UpdateSizePosInfo() = {
		(this.IsIconized() || this.IsMaximized()) && return
		[g.xColorAdjustDialog, g.yColorAdjustDialog] = this.GetScreenPositionXY()
	}
	UpdateExtractor() = {
		!this.extractor && return
		this.extractor.Refresh()
	}
}

//-----------------------------------------------------------------------------
// LayoutFrameEditorDialog
//-----------------------------------------------------------------------------
LayoutFrameEditorDialog = class(wx.Dialog) {
	wdSpin = 80
	htFrameMax = 100	// unit: mm
	wdFrameMax = 60		// unit: mm
	__init__(parent:wx.Window, extractor:Extractor,  productViewer:ProductViewer,
				layoutFrame:LayoutFrame) = {|parent, wx.ID_ANY, 'Layout Frame Editor'$,
						 pos => wx.Point(g.xLayoutFrameEditorDialog, g.yLayoutFrameEditorDialog),|
		this.extractor = extractor
		this.productViewer = productViewer
		this.layoutFrame = layoutFrame
		this.purpose_init		= this.layoutFrame.purpose
		this.htFrame_init		= this.layoutFrame.htFrame
		this.wdFrame_init		= this.layoutFrame.wdFrame
		this.htUpperMgn_init	= this.layoutFrame.htUpperMgn
		this.htFacePart_init	= this.layoutFrame.htFacePart
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.LEFT | wx.RIGHT, 8))
		vbox.AddSpacer(16)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Expand())
			wx.StaticText(this, wx.ID_ANY, 'Purpose'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Centre())
			}
			wx.TextCtrl(this, wx.ID_ANY, layoutFrame.purpose) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags(1).Centre().Border(wx.LEFT, 16))
				this.textPurpose = ctrl
				ctrl.Bind(wx.EVT_TEXT) {
					this.layoutFrame.purpose = this.textPurpose.GetValue()
				}
			}
		}
		vbox.AddSpacer(16)
		wx.StaticBox(this, wx.ID_ANY, 'Frame Size'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand())
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vboxIn.Add(hbox, wx.SizerFlags().Border(wx.ALL, 8))
				wx.StaticText(stb, wx.ID_ANY, 'Height'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre())
				}
				wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
						style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
						min => 10, max => htFrameMax, initial => layoutFrame.htFrame) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8).Centre())
					this.spinFrameHeight = ctrl
					ctrl.Bind(wx.EVT_SPINCTRL) {|event|
						// you can't use event.GetPosition() with Gura v0.5.1
						this.layoutFrame.htFrame = ctrl.GetValue()
						if (this.rbSettingByProportion.GetValue()) {
							this.layoutFrame.htUpperMgn = int(this.layoutFrame.htFrame * \
										this.spinPropUpperMgn.GetValue() / 100)
							this.layoutFrame.htFacePart = max(int(this.layoutFrame.htFrame * \
										this.spinPropFacePart.GetValue() / 100), 1)
							this.spinLengthUpperMgn.SetValue(this.layoutFrame.htUpperMgn)
							this.spinLengthFacePart.SetValue(this.layoutFrame.htFacePart)
						}
						this.UpdateExtractor()
						this.CheckValidity()
					}
				}
				wx.StaticText(stb, wx.ID_ANY, 'mm'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Centre())
				}
				wx.StaticText(stb, wx.ID_ANY, 'Width'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16).Centre())
				}
				wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
						style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
						min => 10, max => wdFrameMax, initial => layoutFrame.wdFrame) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8).Centre())
					this.spinFrameWidth = ctrl
					ctrl.Bind(wx.EVT_SPINCTRL) {|event|
						// you can't use event.GetPosition() with Gura v0.5.1
						this.layoutFrame.wdFrame = ctrl.GetValue()
						this.UpdateExtractor()
					}
				}
				wx.StaticText(stb, wx.ID_ANY, 'mm'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Centre())
				}
			}
		}
		vbox.AddSpacer(16)
		wx.StaticBox(this, wx.ID_ANY, 'Allocation of Upper Margin and Face Part'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags())
			wx.FlexGridSizer(nil, 3, 16, 16) {|gbox|
				vboxIn.Add(gbox, wx.SizerFlags().Border(wx.ALL, 8))
				wx.RadioButton(stb, wx.ID_ANY, 'Setting by Proportion'$, style => wx.RB_GROUP) {|ctrl|
					gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.rbSettingByProportion = ctrl
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags())
					wx.StaticText(stb, wx.ID_ANY, 'Upper Margin'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 0, max => 100,
							initial => int(layoutFrame.htUpperMgn * 100 / layoutFrame.htFrame)) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8).Centre())
						this.spinPropUpperMgn = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.layoutFrame.htUpperMgn = int(this.layoutFrame.htFrame * ctrl.GetValue() / 100)
							this.spinLengthUpperMgn.SetValue(this.layoutFrame.htUpperMgn)
							this.UpdateExtractor()
							this.CheckValidity()
						}
						ctrl.Bind(wx.EVT_UPDATE_UI) {|event|
							event.Enable(this.rbSettingByProportion.GetValue())
						}
					}
					wx.StaticText(stb, wx.ID_ANY, '%'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Centre())
					}
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags())
					wx.StaticText(stb, wx.ID_ANY, 'Face Part'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 1, max => 100,
							initial => int(layoutFrame.htFacePart * 100 / layoutFrame.htFrame)) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8).Centre())
						this.spinPropFacePart = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.layoutFrame.htFacePart = max(int(this.layoutFrame.htFrame * ctrl.GetValue() / 100), 1)
							this.spinLengthFacePart.SetValue(this.layoutFrame.htFacePart)
							this.UpdateExtractor()
							this.CheckValidity()
						}
						ctrl.Bind(wx.EVT_UPDATE_UI) {|event|
							event.Enable(this.rbSettingByProportion.GetValue())
						}
					}
					wx.StaticText(stb, wx.ID_ANY, '%'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Centre())
					}
				}
				wx.RadioButton(stb, wx.ID_ANY, 'Setting by Length'$) {|ctrl|
					gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
					this.rbSettingByLength = ctrl
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags())
					wx.StaticText(stb, wx.ID_ANY, 'Upper Margin'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 0, max => htFrameMax,
							initial => layoutFrame.htUpperMgn) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8).Centre())
						this.spinLengthUpperMgn = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.layoutFrame.htUpperMgn = ctrl.GetValue()
							ratio = int(this.layoutFrame.htUpperMgn * 100 / this.layoutFrame.htFrame)
							this.spinPropUpperMgn.SetValue(ratio)
							this.UpdateExtractor()
							this.CheckValidity()
						}
						ctrl.Bind(wx.EVT_UPDATE_UI) {|event|
							event.Enable(this.rbSettingByLength.GetValue())
						}
					}
					wx.StaticText(stb, wx.ID_ANY, 'mm'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Centre())
					}
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags())
					wx.StaticText(stb, wx.ID_ANY, 'Face Part'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 1, max => htFrameMax,
							initial => layoutFrame.htFacePart) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8).Centre())
						this.spinLengthFacePart = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.layoutFrame.htFacePart = ctrl.GetValue()
							ratio = int(this.layoutFrame.htFacePart * 100 / this.layoutFrame.htFrame)
							this.spinPropFacePart.SetValue(ratio)
							this.UpdateExtractor()
							this.CheckValidity()
						}
						ctrl.Bind(wx.EVT_UPDATE_UI) {|event|
							event.Enable(this.rbSettingByLength.GetValue())
						}
					}
					wx.StaticText(stb, wx.ID_ANY, 'mm'$) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Centre())
					}
				}
			}
		}
		outerBox.AddSpacer(12)
		wx.StaticText(this, wx.ID_ANY, '', style => wx.ST_NO_AUTORESIZE) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT | wx.RIGHT , 8))
			this.labelIndicator = ctrl
		}
		outerBox.AddSpacer(12)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			outerBox.Add(hbox, wx.SizerFlags().Centre().Border(wx.TOP, 8))
			wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
				ctrl.SetDefault()
				this.btnOK = ctrl
			}
			wx.Button(this, wx.ID_CANCEL, 'Cancel'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16))
			}
		}
		this.textPurpose.SetFocus()
		this.Bind(wx.EVT_MOVE) {|event| this.UpdateSizePosInfo(), event.Skip()}
		this.Bind(wx.EVT_CLOSE) {|event| this.OnClose(event)}
		this.Bind(wx.EVT_BUTTON, wx.ID_OK) {|event| this.OnButton_OK(event)}
		this.Bind(wx.EVT_BUTTON, wx.ID_CANCEL) {|event| this.OnButton_CANCEL(event)}
		outerBox.AddSpacer(8)
		this.Fit()
		this.frostFrameFlagSaved = g.frostFrameFlag
		g.frostFrameFlag = true
		this.UpdateExtractor()
	}
	OnClose(event:wx.CloseEvent) = {
		g.frostFrameFlag = this.frostFrameFlagSaved
		this.CancelModification()
		this.EndModal(wx.ID_CANCEL)
	}
	OnButton_OK(event:wx.CommandEvent) = {
		g.frostFrameFlag = this.frostFrameFlagSaved
		this.UpdateExtractor()
		this.UpdateProductViewer()
		this.EndModal(wx.ID_OK)
	}
	OnButton_CANCEL(event:wx.CommandEvent) = {
		g.frostFrameFlag = this.frostFrameFlagSaved
		this.CancelModification()
		this.EndModal(wx.ID_CANCEL)
	}
	UpdateSizePosInfo() = {
		(this.IsIconized() || this.IsMaximized()) && return
		[g.xLayoutFrameEditorDialog, g.yLayoutFrameEditorDialog] = this.GetScreenPositionXY()
	}
	CancelModification() = {
		this.layoutFrame.purpose	= this.purpose_init
		this.layoutFrame.htFrame	= this.htFrame_init
		this.layoutFrame.wdFrame	= this.wdFrame_init
		this.layoutFrame.htUpperMgn	= this.htUpperMgn_init
		this.layoutFrame.htFacePart	= this.htFacePart_init
		this.UpdateExtractor()
	}
	UpdateExtractor() = {
		!this.extractor && return
		this.extractor.Refresh()
	}
	UpdateProductViewer() = {
		!this.productViewer && return
		wx.BusyCursor {
			this.productViewer.UpdateContent()
		}
	}
	CheckValidity() = {
		validFlag = (this.layoutFrame.htUpperMgn + this.layoutFrame.htFacePart <= this.layoutFrame.htFrame)
		this.btnOK.Enable(validFlag)
		this.labelIndicator.SetLabel(cond(validFlag, '',
			'The length of upper margin and face part exceeds the whole frame.'$))
	}
}

//-----------------------------------------------------------------------------
// OutputOptionDialog
//-----------------------------------------------------------------------------
OutputOptionDialog = class(wx.Dialog) {
	wdSpin = 60
	__init__(parent:wx.Window, productViewer:ProductViewer) = {|parent, wx.ID_ANY, 'Output Option'$,
				pos => wx.Point(g.xOutputOptionDialog, g.yOutputOptionDialog),
				style => (wx.DEFAULT_DIALOG_STYLE | wx.STAY_ON_TOP)|
		this.productViewer = productViewer
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.LEFT | wx.RIGHT, 8))
		this.orientation_init				= g.orientation
		this.wdEdgeMgn_init					= g.wdEdgeMgn
		this.htEdgeMgn_init					= g.htEdgeMgn
		this.trimMark_init					= g.trimMark
		this.trimMarkR_init					= g.trimMarkR
		this.trimMarkG_init					= g.trimMarkG
		this.trimMarkB_init					= g.trimMarkB
		this.titleDispFlag_Size_init		= g.titleDispFlag_Size
		this.titleDispFlag_ImageLabel_init	= g.titleDispFlag_ImageLabel
		this.titleDispFlag_Purpose_init		= g.titleDispFlag_Purpose
		vbox.AddSpacer(12)
		wx.StaticBox(this, wx.ID_ANY, 'Paper Orientation'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand())
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vboxIn.Add(hbox, wx.SizerFlags().Border(wx.ALL, 8))
				wx.RadioButton(stb, wx.ID_ANY, 'Auto'$, style => wx.RB_GROUP) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre())
					this.rbPaperOrient_Auto = ctrl
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						g.orientation = `auto
						this.UpdateProductViewer()
					}
				}
				wx.RadioButton(stb, wx.ID_ANY, 'Vertical'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 20))
					this.rbPaperOrient_Vertical = ctrl
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						g.orientation = `vertical
						this.UpdateProductViewer()
					}
				}
				wx.RadioButton(stb, wx.ID_ANY, 'Horizontal'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 20))
					this.rbPaperOrient_Horizontal = ctrl
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						g.orientation = `horizontal
						this.UpdateProductViewer()
					}
				}
			}
		}
		vbox.AddSpacer(12)
		wx.StaticBox(this, wx.ID_ANY, 'Edge Margin'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand())
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vboxIn.Add(hbox, wx.SizerFlags().Border(wx.ALL, 8))
				wx.StaticText(stb, wx.ID_ANY, 'Horizontal'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
						style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
						min => 0, max => 10, initial => 0) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8))
					this.spinEdgeMgn_Horizontal = ctrl
					ctrl.Bind(wx.EVT_SPINCTRL) {|event|
						// you can't use event.GetPosition() with Gura v0.5.1
						g.wdEdgeMgn = ctrl.GetValue()
						this.UpdateProductViewer()
					}
				}
				wx.StaticText(stb, wx.ID_ANY, 'mm'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.StaticText(stb, wx.ID_ANY, 'Vertical'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16).Align(wx.ALIGN_CENTRE_VERTICAL))
				}
				wx.SpinCtrl(stb, wx.ID_ANY, size => wx.Size(wdSpin, -1),
						style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
						min => 0, max => 10, initial => 0) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8))
					this.spinEdgeMgn_Vertical = ctrl
					ctrl.Bind(wx.EVT_SPINCTRL) {|event|
						// you can't use event.GetPosition() with Gura v0.5.1
						g.htEdgeMgn = ctrl.GetValue()
						this.UpdateProductViewer()
					}
				}
				wx.StaticText(stb, wx.ID_ANY, 'mm'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Align(wx.ALIGN_CENTRE_VERTICAL))
				}
			}
		}
		vbox.AddSpacer(12)
		wx.StaticBox(this, wx.ID_ANY, 'Trim Mark'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand())
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vboxIn.Add(hbox, wx.SizerFlags().Border(wx.ALL, 8))
				wx.StaticText(stb, wx.ID_ANY, 'Line Color'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre())
				}
				wx.ColourPickerCtrl(stb, wx.ID_ANY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 12))
					this.clrPickerTrimMarkLineColor = ctrl
					ctrl.Bind(wx.EVT_UPDATE_UI) {|event|
						event.Enable(g.trimMark != `none)
					}
					ctrl.Bind(wx.EVT_COLOURPICKER_CHANGED) {
						c = ctrl.GetColour()
						g.trimMarkR = c.Red()
						g.trimMarkG = c.Green()
						g.trimMarkB = c.Blue()
						this.UpdateProductViewer()
					}
				}
				hbox.AddSpacer(20)
				wx.RadioButton(stb, wx.ID_ANY, 'Outer'$, style => wx.RB_GROUP) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre())
					this.rbTrimMark_Outer = ctrl
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						g.trimMark = `outer
						this.UpdateProductViewer()
					}
				}
				wx.RadioButton(stb, wx.ID_ANY, 'Overwrite'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 20))
					this.rbTrimMark_Overwrite = ctrl
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						g.trimMark = `overwrite
						this.UpdateProductViewer()
					}
				}
				wx.RadioButton(stb, wx.ID_ANY, 'None'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 20))
					this.rbTrimMark_None = ctrl
					ctrl.Bind(wx.EVT_RADIOBUTTON) {
						g.trimMark = `none
						this.UpdateProductViewer()
					}
				}
			}
		}
		vbox.AddSpacer(12)
		wx.StaticBox(this, wx.ID_ANY, 'Title Output'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand())
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vboxIn.Add(hbox, wx.SizerFlags().Border(wx.ALL, 8))
				wx.CheckBox(stb, wx.ID_ANY, 'Frame Size'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre())
					this.chkTitle_Size = ctrl
					ctrl.Bind(wx.EVT_CHECKBOX) {|event|
						g.titleDispFlag_Size = event.IsChecked()
						this.UpdateProductViewer()
					}
				}
				wx.CheckBox(stb, wx.ID_ANY, 'Image Label'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 20))
					this.chkTitle_ImageLabel = ctrl
					ctrl.Bind(wx.EVT_CHECKBOX) {|event|
						g.titleDispFlag_ImageLabel = event.IsChecked()
						this.UpdateProductViewer()
					}
				}
				wx.CheckBox(stb, wx.ID_ANY, 'Purpose'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 20))
					this.chkTitle_Purpose = ctrl
					ctrl.Bind(wx.EVT_CHECKBOX) {|event|
						g.titleDispFlag_Purpose = event.IsChecked()
						this.UpdateProductViewer()
					}
				}
			}
		}
		outerBox.AddSpacer(16)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			outerBox.Add(hbox, wx.SizerFlags().Centre().Border(wx.TOP, 8))
			wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
				ctrl.SetDefault()
				ctrl.SetFocus()
			}
			wx.Button(this, wx.ID_CANCEL, 'Cancel'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16))
			}
		}
		outerBox.AddSpacer(8)
		this.Fit()
		this.Bind(wx.EVT_MOVE) {|event| this.UpdateSizePosInfo(), event.Skip()}
		this.Bind(wx.EVT_BUTTON, wx.ID_OK) {|event| this.OnButton_OK(event)}
		this.Bind(wx.EVT_BUTTON, wx.ID_CANCEL) {|event| this.OnButton_CANCEL(event)}
		this.Bind(wx.EVT_CLOSE) {|event| this.OnClose(event)}
	}
	InitWindowValues() = {
		this.rbPaperOrient_Auto.SetValue(g.orientation == `auto)
		this.rbPaperOrient_Vertical.SetValue(g.orientation == `vertical)
		this.rbPaperOrient_Horizontal.SetValue(g.orientation == `horizontal)
		this.spinEdgeMgn_Horizontal.SetValue(g.wdEdgeMgn)
		this.spinEdgeMgn_Vertical.SetValue(g.htEdgeMgn)
		this.rbTrimMark_Outer.SetValue(g.trimMark == `outer)
		this.rbTrimMark_Overwrite.SetValue(g.trimMark == `overwrite)
		this.rbTrimMark_None.SetValue(g.trimMark == `none)
		this.clrPickerTrimMarkLineColor.SetColour(
					wx.Colour(g.trimMarkR, g.trimMarkG, g.trimMarkB))
		this.chkTitle_Size.SetValue(g.titleDispFlag_Size)
		this.chkTitle_ImageLabel.SetValue(g.titleDispFlag_ImageLabel)
		this.chkTitle_Purpose.SetValue(g.titleDispFlag_Purpose)
	}
	OnClose(event:wx.CloseEvent) = {
		this.CancelModification()
		this.Show(false)
	}
	OnButton_OK(event:wx.CommandEvent) = {
		this.Show(false)
	}
	OnButton_CANCEL(event:wx.CommandEvent) = {
		this.CancelModification()
		this.Show(false)
	}
	CancelModification() = {
		g.orientation				= this.orientation_init
		g.wdEdgeMgn					= this.wdEdgeMgn_init
		g.htEdgeMgn					= this.htEdgeMgn_init
		g.trimMark					= this.trimMark_init
		g.trimMarkR					= this.trimMarkR_init
		g.trimMarkG					= this.trimMarkG_init
		g.trimMakrsB				= this.trimMarkB_init
		g.titleDispFlag_Size		= this.titleDispFlag_Size_init
		g.titleDispFlag_ImageLabel	= this.titleDispFlag_ImageLabel_init
		g.titleDispFLag_Purpose		= this.titleDispFlag_Purpose_init
		this.UpdateProductViewer()
	}
	UpdateSizePosInfo() = {
		(this.IsIconized() || this.IsMaximized()) && return
		[g.xOutputOptionDialog, g.yOutputOptionDialog] = this.GetScreenPositionXY()
	}
	UpdateProductViewer() = {
		!this.productViewer && return
		wx.BusyCursor {
			this.productViewer.UpdateContent()
		}
	}
}

//-----------------------------------------------------------------------------
// PrintServiceDialog
//-----------------------------------------------------------------------------
PrintServiceDialog = class(wx.Dialog) {
	textMsg = \
	'For exact sizing, enter numbers of adjustment lines printed alongside of long and short side of paper.'$
	__init__(parent:wx.Window, nAdjLinesDrawn:number, printServiceName:string,
			nAdjLines_a:number, nAdjLines_b:number,
			nAdjLines_c:number, nAdjLines_d:number) = {|parent, wx.ID_ANY, 'Print Service'$|
		this.SetIcon(g.icon)
		this.printServiceName:public = printServiceName
		this.nAdjLines_a:public = nAdjLines_a
		this.nAdjLines_b:public = nAdjLines_b
		this.nAdjLines_c:public = nAdjLines_c
		this.nAdjLines_d:public = nAdjLines_d
		wdSymbol = 30
		wdSpin = 80
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.LEFT | wx.RIGHT, 8))
		vbox.AddSpacer(16)
		wx.StaticText(this, wx.ID_ANY, textMsg) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Centre())
		}
		vbox.AddSpacer(8)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Centre())
			wx.StaticText(this, wx.ID_ANY,
				'Detail information about adjustment lines:'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Centre())
			}
			text = 'Print Service Function'$
			url = 'http://app.gura-lang.org/gurashot/#print-service'
			wx.HyperlinkCtrl(this, wx.ID_ANY, text, url) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8))
			}
		}
		vbox.AddSpacer(16)
		wx.StaticBitmap(this, wx.ID_ANY, wx.Bitmap(g.imgPrintServiceAdjust),
											style => wx.BORDER_STATIC) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Centre())
		}
		vbox.AddSpacer(12)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Expand())
			wx.StaticText(this, wx.ID_ANY, 'Name of Print Service'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.TextCtrl(this, wx.ID_ANY, this.printServiceName) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags(1).Border(wx.LEFT, 8).Align(wx.ALIGN_CENTRE_VERTICAL))
				this.textPrintServiceName = ctrl
			}
			wx.Button(this, wx.ID_ANY, 'Browse'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4).Expand())
				ctrl.Bind(wx.EVT_BUTTON) {|event| this.BrowsePrintService()}
			}
		}
		vbox.AddSpacer(12)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Centre())
			wx.FlexGridSizer(nil, 3, 8, 12) {|gbox|
				hbox.Add(gbox, wx.SizerFlags().Centre())
				wx.StaticText(this, wx.ID_ANY, 'Num of Adjust Lines on Long Side'$) {|ctrl|
					gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.RIGHT, 12))
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags().Centre())
					wx.StaticText(this, wx.ID_ANY, 'a =', size => wx.Size(wdSymbol, -1)) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(this, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 0, max => nAdjLinesDrawn, initial => this.nAdjLines_a) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags())
						this.spinAdjLines_a = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.nAdjLines_a = ctrl.GetValue()
						}
					}
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags().Centre())
					wx.StaticText(this, wx.ID_ANY, 'b =', size => wx.Size(wdSymbol, -1)) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(this, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 0, max => nAdjLinesDrawn, initial => this.nAdjLines_b) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags())
						this.spinAdjLines_b = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.nAdjLines_b = ctrl.GetValue()
						}
					}
				}
				wx.StaticText(this, wx.ID_ANY, 'Num of Adjust Lines on Short Side'$) {|ctrl|
					gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.RIGHT, 12))
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags().Centre())
					wx.StaticText(this, wx.ID_ANY, 'c =', size => wx.Size(wdSymbol, -1)) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(this, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 0, max => nAdjLinesDrawn, initial => this.nAdjLines_c) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags())
						this.spinAdjLines_c = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.nAdjLines_c = ctrl.GetValue()
						}
					}
				}
				wx.BoxSizer(wx.HORIZONTAL) {|hbox|
					gbox.Add(hbox, wx.SizerFlags().Centre())
					wx.StaticText(this, wx.ID_ANY, 'd =', size => wx.Size(wdSymbol, -1)) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags().Centre())
					}
					wx.SpinCtrl(this, wx.ID_ANY, size => wx.Size(wdSpin, -1),
							style => wx.ALIGN_CENTRE_HORIZONTAL | wx.SP_ARROW_KEYS,
							min => 0, max => nAdjLinesDrawn, initial => this.nAdjLines_d) {|ctrl|
						hbox.Add(ctrl, wx.SizerFlags())
						this.spinAdjLines_d = ctrl
						ctrl.Bind(wx.EVT_SPINCTRL) {|event|
							// you can't use event.GetPosition() with Gura v0.5.1
							this.nAdjLines_d = ctrl.GetValue()
						}
					}
				}
			}
			hbox.AddSpacer(32)
			wx.Button(this, wx.ID_OK, 'Output'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				ctrl.SetDefault()
				ctrl.SetFocus()
			}
		}
		outerBox.AddSpacer(16)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			outerBox.Add(hbox, wx.SizerFlags().Centre().Border(wx.TOP, 8))
			wx.Button(this, wx.ID_CANCEL, 'Close'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
			}
		}
		outerBox.AddSpacer(8)
		this.Fit()
	}
	BrowsePrintService() = {
		dlg = PrintServiceBrowserDialog(this)
		dlg.CentreOnParent()
		if (dlg.ShowModal() == wx.ID_OK) {
			this.printServiceName = dlg.printServiceInfo.printServiceName
			this.nAdjLines_a = dlg.printServiceInfo.nAdjLines_a
			this.nAdjLines_b = dlg.printServiceInfo.nAdjLines_b
			this.nAdjLines_c = dlg.printServiceInfo.nAdjLines_c
			this.nAdjLines_d = dlg.printServiceInfo.nAdjLines_d
			this.textPrintServiceName.SetValue(this.printServiceName)
			this.spinAdjLines_a.SetValue(this.nAdjLines_a)
			this.spinAdjLines_b.SetValue(this.nAdjLines_b)
			this.spinAdjLines_c.SetValue(this.nAdjLines_c)
			this.spinAdjLines_d.SetValue(this.nAdjLines_d)
		}
	}
}

//-----------------------------------------------------------------------------
// PrintServiceBrowserDialog
//-----------------------------------------------------------------------------
PrintServiceBrowserDialog = class(wx.Dialog) {
	[wdList, htList] = [540, 300]
	__init__(parent:wx.Window) = {|parent, wx.ID_ANY, 'Print Service Browser'$|
		this.SetIcon(g.icon)
		this.printServiceInfo:public = nil
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags().Border(wx.ALL, 8))
		wx.ListView(this, wx.ID_ANY, size => wx.Size(wdList, htList),
					style => wx.BORDER_SUNKEN | wx.LC_REPORT | wx.LC_SINGLE_SEL) {|ctrl|
			font = ctrl.GetFont()
			vbox.Add(ctrl, wx.SizerFlags())
			font.SetPointSize(font.GetPointSize() + 2)
			ctrl.SetFont(font)
			ctrl.InsertColumn(0.., ['Print Service'$, 'Device Name etc.'$])
			ctrl.Bind(wx.EVT_SIZE) {|event|
				size = event.GetSize()
				width = size.GetWidth() - 25
				wdColumn = width / 2
				ctrl.SetColumnWidth(0, wdColumn)
				width -= wdColumn
				ctrl.SetColumnWidth(1, width)
				event.Skip()
			}
			g.printServiceInfoTbl.each {|printServiceInfo, idx|
				fields = printServiceInfo.printServiceName.split('|'):list
				wx.ListItem {|item|
					item.SetId(idx)
					item.SetText(fields[0].strip())
					ctrl.InsertItem(item)
				}
				if (fields.len() > 1) {
					wx.ListItem {|item|
						item.SetId(idx)
						item.SetColumn(1)
						item.SetText(fields[1].strip())
						ctrl.SetItem(item)
					}
				}
			}
			ctrl.SetItemState(0,
					wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED,
					wx.LIST_STATE_FOCUSED | wx.LIST_STATE_SELECTED)
			ctrl.Bind(wx.EVT_LIST_ITEM_ACTIVATED) {|event| this.OnListItemActivated(event) }
			this.listPrintService = ctrl
		}
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			outerBox.Add(hbox, wx.SizerFlags().Centre().Border(wx.TOP, 8))
			wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
				ctrl.SetDefault()
				ctrl.SetFocus()
			}
			wx.Button(this, wx.ID_CANCEL, 'Cancel'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 16))
			}
		}
		outerBox.AddSpacer(8)
		this.Fit()
		this.listPrintService.SetFocus()
		this.Bind(wx.EVT_BUTTON, wx.ID_OK) {|event| this.OnButton_OK(event)}
	}
	OnButton_OK(event:wx.CommandEvent) = {
		((idx = this.listPrintService.GetFirstSelected()) < 0) && return
		this.printServiceInfo = g.printServiceInfoTbl[idx]
		this.EndModal(wx.ID_OK)
	}
	OnListItemActivated(event:wx.ListEvent) = {
		((idx = event.GetIndex()) < 0) && return
		this.printServiceInfo = g.printServiceInfoTbl[idx]
		this.EndModal(wx.ID_OK)
	}
}

//-----------------------------------------------------------------------------
// RenameLabelDialog
//-----------------------------------------------------------------------------
RenameLabelDialog = class(wx.Dialog) {
	wdText = 200
	__init__(parent:wx.Window, imgInfo:ImageInfo) = {|parent, wx.ID_ANY, 'Rename Label'$|
		this.imgInfo = imgInfo
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 8))
		wx.FlexGridSizer(nil, 2, 8, 16) {|gbox|
			vbox.Add(gbox, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			wx.StaticText(this, wx.ID_ANY, 'Original Name'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.StaticText(this, wx.ID_ANY, imgInfo.label,
						size => wx.Size(wdText, -1), style => wx.BORDER_SUNKEN) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.StaticText(this, wx.ID_ANY, 'Modified Name'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.TextCtrl(this, wx.ID_ANY, imgInfo.label, size => wx.Size(wdText, -1)) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
				this.ctrlText = ctrl
			}
		}
		vbox.AddSpacer(16)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Centre().Border(wx.TOP | wx.BOTTOM, 4))
			wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Centre())
				ctrl.SetDefault()
			}
			wx.Button(this, wx.ID_CANCEL, 'Cancel'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.LEFT, 8))
			}
		}
		this.Fit()
		this.ctrlText.SetFocus()
		this.Bind(wx.EVT_BUTTON, wx.ID_OK) {|event| this.OnButton_OK(event)}
	}
	OnButton_OK(event:wx.CommandEvent) = {
		this.imgInfo.label = this.ctrlText.GetValue()
		this.EndModal(wx.ID_OK)
	}
}

//-----------------------------------------------------------------------------
// AboutDialog
//-----------------------------------------------------------------------------
AboutDialog = class(wx.Dialog) {
	__init__(parent:wx.Window) = {|parent, wx.ID_ANY, 'About'$, size => wx.Size(320, 200)|
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		vbox.AddStretchSpacer(1)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Centre())
			wx.StaticBitmap(this, wx.ID_ANY, wx.NullBitmap) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
				ctrl.SetIcon(g.icon)
			}
			wx.BoxSizer(wx.HORIZONTAL) {|hboxIn|
				hbox.Add(hboxIn, wx.SizerFlags().Centre().Border(wx.LEFT, 12))
				hbox = hboxIn
				wx.StaticText(this, wx.ID_ANY, 'Gura Shot'$) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags())
					font = ctrl.GetFont()
					font.SetPointSize(14)
					font.SetWeight(wx.FONTWEIGHT_BOLD)
					ctrl.SetFont(font)
				}
				wx.StaticText(this, wx.ID_ANY, 'Version ' + Version) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_BOTTOM).Border(wx.LEFT, 12))
				}
			}
		}
		vbox.AddSpacer(8)
		wx.StaticText(this, wx.ID_ANY, 'Copyright (C) 2014 ypsitau') {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Centre())
		}
		vbox.AddStretchSpacer(1)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Centre())
			url = 'http://app.gura-lang.org/gurashot/'
			wx.HyperlinkCtrl(this, wx.ID_ANY, url, url) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 4))
			}
		}
		vbox.AddStretchSpacer(1)
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Centre().Border(wx.TOP | wx.BOTTOM, 4))
			ctrl.SetDefault()
			ctrl.SetFocus()
		}
	}
}

//-----------------------------------------------------------------------------
// App
//-----------------------------------------------------------------------------
App = class(wx.App) {
	OnInit() = {
		LoadConfig()
		InitializeResource()
		UpdateImagePhoto()
		mainWindow = MainWindow('Gura Shot'$)
		mainWindow.Show()
		g.mainWindow = mainWindow
		true
	}
	OnExit() = {
		SaveConfig()
		0
	}
}

wx.IMPLEMENT_APP(App)
